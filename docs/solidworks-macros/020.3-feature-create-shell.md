---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Create Shell
permalink: /solidworks-vba-macros/create-shell/
tags:   [Solidworks Macro]
id: 'feature-create-shell'
---

{% include article-notes/intermediate-note.html %}

## Objective

In this article I want to:

* Create Shell feature
* From VBA Macro
* Without using Userforms.

This method is ***most updated*** method, so ***use this method*** if you want to create a new **Shell Feature** quickly. 

## Results We Can Get

After running our macro we successfully create **Shell feature** as a result.

Below image shows the result we get.

[![shell-feature-final-result](/assets/Solidworks_Images/feature-shell/final-result-gif.gif)](/assets/Solidworks_Images/feature-shell/final-result-gif.gif)

We create **Shell Feature** in following steps in general.

1. *Ask user to select **Faces to Shell **.*
3. *Ask for **Shell Thickness**.*

To get the correct result please follow the steps correctly.
{: .notice--warning}

## Macro Video

Below üé¨ video shows **Shell feature** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/IoNnQ8i-Ybw" frameborder="0" allowfullscreen></iframe>



***Above video is just for visualization and there are no explanation.*** 

***I have explained each and every line in this article.***

It is advisable to watch video, since it help you to better understand the process.
{: .notice--warning}

## VBA Macro

Below is the ***VBA macro*** for creating *Shell Feature*.

```vb
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Selection Manager
Dim swSelMgr As SldWorks.SelectionMgr

' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData

' Main program for Shell
Sub main()

  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Check if Solidworks is opened or not
  If swApp Is Nothing Then
    MsgBox ("Solidworks is not opened")
    Exit Sub
  End If
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox ("Solidworks document is not opened. Please open a document.")
    Exit Sub
  End If
  
  ' Set Solidworks Selection Manager variable
  Set swSelMgr = swDoc.SelectionManager
  
  ' Variable for Solidworks Entity
  Dim swObject As SldWorks.Entity
  
  ' Message to show user
  Dim messageToUser As String

  ' Update Messages
  messageToUser = "Please select a Face."

  ' Show message to user
  MsgBox messageToUser
  
  ' Loop until we complete our selection
  While swObject Is Nothing
      
    ' Local integer for loop
    Dim i As Integer
    
    ' Looping until we select
    For i = 1 To swSelMgr.GetSelectedObjectCount2(-1)
      
      ' If the selection type is face
      If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then
          
        ' Set the Solidworks Entity object to feature
        Set swObject = swSelMgr.GetSelectedObject6(i, -1)
          
      Else

        ' Inform user to select a Face
        MsgBox "Please select a Face."

        ' Clear selection
        swDoc.ClearSelection2 True
      End If
    Next
    
    DoEvents
  Wend
    
  ' Clear previous selection
  swDoc.ClearSelection2 True
  
  ' Create Select data for this entity
  Set swSelData = swSelMgr.CreateSelectData
  
  ' For shell face, set mark to 1
  swSelData.Mark = 1

  ' Select the current entity
  swObject.Select4 True, swSelData
  
  ' Local variables used as Conversion Factors
  Dim LengthConversionFactor As Double
  Dim AngleConversionFactor As Double
  
  ' Use a Select Case, to get the length of active Unit and set the different factors
  Select Case swDoc.GetUnits(0)       ' GetUnits function gives us, active unit
    
    Case swMETER    ' If length is in Meter
      LengthConversionFactor = 1
      AngleConversionFactor = 1
    
    Case swMM       ' If length is in MM
      LengthConversionFactor = 1 / 1000
      AngleConversionFactor = 1 * 0.01745329
    
    Case swCM       ' If length is in CM
      LengthConversionFactor = 1 / 100
      AngleConversionFactor = 1 * 0.01745329
    
    Case swINCHES   ' If length is in INCHES
      LengthConversionFactor = 1 * 0.0254
      AngleConversionFactor = 1 * 0.01745329
    
    Case swFEET     ' If length is in FEET
      LengthConversionFactor = 1 * (0.0254 * 12)
      AngleConversionFactor = 1 * 0.01745329
    
    Case swFEETINCHES     ' If length is in FEET & INCHES
      LengthConversionFactor = 1 * 0.0254  ' For length we use sama as Inch
      AngleConversionFactor = 1 * 0.01745329
    
    Case swANGSTROM        ' If length is in ANGSTROM
      LengthConversionFactor = 1 / 10000000000#
      AngleConversionFactor = 1 * 0.01745329
    
    Case swNANOMETER       ' If length is in NANOMETER
      LengthConversionFactor = 1 / 1000000000
      AngleConversionFactor = 1 * 0.01745329
    
    Case swMICRON       ' If length is in MICRON
      LengthConversionFactor = 1 / 1000000
      AngleConversionFactor = 1 * 0.01745329
  End Select
  
  ' Variable for Shell thickness
  Dim shellThickness As Double
  
  ' Getting Shell thickness from user.
  shellThickness = InputBox("Please select Shell Thickness.") * LengthConversionFactor
  
  ' Create Shell feature
  swDoc.InsertFeatureShell shellThickness, False
  
  ' View zoom to fit
  swDoc.ViewZoomtofit2
  
  ' Clear all selection
  swDoc.ClearSelection2 True

End Sub
```

## Prerequisite

There are some *prerequisite* for this macro.

* Knowledge of **VBA programming language** is ‚ùó***required***.
* We are **not** creating feature from code but we use existing üöÄ **[Extrude feature](/solidworks-macros/create-extrude-feature)** to create **Shell feature**.

*Below image shown prerequisite 3D model for our demo.*

[![prerequisite](/assets/Solidworks_Images/feature-shell/prerequisite.png)](/assets/Solidworks_Images/feature-shell/prerequisite.png)

As shown in above image, there is ***one Extrude features*** in our part.

- `Base Feature` : This is our **Base Extrude feature**.


If you want to create this **Extrude** features ***programmatically*** then please refer to below article.

* üöÄ **[SOLIDWORKS Macros - Create Extrude/Boss](/solidworks-macros/create-extrude-feature)**

We will apply checks in this article, so the code we write should be error free most of the time.
{: .notice}

## Steps To Follow

This **Shell Feature VBA macro** can be divided into following sections:

1. *Creating Global Variables*
2. *Initializing required variables*
3. *Ask user to select faces*
4. *Mark selected entities*
5. *Get unit Conversion factors*
6. *Get Shell Thickness*
7. *Create Shell feature*
8. *Final work*

***Every section with each line is explained below.***

I also give some ***links (see icon üöÄ)*** so that you can go through them if there are anything I explained in previous articles.
{: .notice}

### Creating Global Variables

```vb
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: üöÄ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for Solidworks document. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb
' Variable for Solidworks Selection Manager
Dim swSelMgr As SldWorks.SelectionMgr
```

* **Purpose**:  In above line, we create a variable for Solidworks Selection Manager.
* **Variable Name**: `swSelMgr` 
* **Type**: `SldWorks.SelectionMgr`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr_members.html)**.

```vb
' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData
```

* **Purpose**: In above line, we create a variable for **Marking** of Solidworks Select Data.
* **Variable Name**: `swSelData`
* **Type**: `SldWorks.SelectData`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectData_members.html)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb
' Main program for Shell
Sub main()

End Sub
```

* In above line, we create *main program for Shell Feature*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

### Initializing Required Variables

Inside this section we initialize required variables.

```vb
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb
' Check if Solidworks is opened or not
If swApp Is Nothing Then  
  MsgBox ("SOLIDWORKS is not opened")  
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swApp`** variable.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swApp Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS is not opened*
  * Then we **stop** our macro here.

```vb
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened. Please open a document.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened. Please open a document.*
  * Then we **stop** our macro here.

```vb
' Set Solidworks Selection Manager variable
Set swSelMgr = swDoc.SelectionManager
```

* In above line, we set *value* of **`swSelMgr`** variable.
* This *value* is current document's *Selection Manager*.

```vb
' Variable for Solidworks Entity
Dim swObjects As SldWorks.Entity
```

* **Purpose**: In above line, we create a variable for *selection and marking* of Solidworks Entity.
* **Variable Name**: `swObject`
* **Type**: `SldWorks.Entity`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**.

### Ask user to select face

In this section, we ask user to select **face** for *Shell feature*.

```vb
' Message to show user
Dim messageToUser As String
```

* In above line, we create a variable to show a messages we want to show before selection.
* **Variable Name**: `messageToUser`
* **Type**: `String`

```vb
' Update Messages
messageToUser = "Please select a Faces."
```

* In above code block, we set value of `messageToUser` variable.
* We set `messageToUser` to `"Please select a Faces."`

```vb
' Show message to user
MsgBox messageToUser
```

* In above line, we show the message to user.
* Below image shows the message for **Face selection** to the user.

[![message-to-select-shell-face](/assets/Solidworks_Images/feature-shell/message-to-select-shell-face.png)](/assets/Solidworks_Images/feature-shell/message-to-select-shell-face.png)

```vb
' Loop until we complete our selection
While swObject Is Nothing    

Wend
```

* In above line, we create another `While` loop.
* This loops continues to run until we select the our **Face** for **Shell feature**.
* **Reference**: üöÄ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Local integer for loop
Dim i As Integer
```

* In above line, we create a variable as a counter.
* **Variable Name**: `i`
* **Type**: `Integer`

```vb
' Loop until we select
For i = 1 To swSelMgr.GetSelectedObjectCount2(-1)

Next
```

* In above line, we create a `For` loop.
* This loops start from `i = 1` to number of `objects` we select.

```vb
' If the selection type is face
If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then

  ' Set the Solidworks Entity object to feature
  Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)

Else

	' Inform user to select a Face
	MsgBox "Please select a Face."

	' Clear selection
	swDoc.ClearSelection2 True
End If
```

* In above code block, we use  üöÄ **[IF/ElseIf statement](/vba/if-then-structure-select-case/)**  with conditions.
* The  üöÄ **[IF statement](/vba/if-then-structure-select-case/)**  is `True` when selected `object` is type of ***SOLIDWORKS Face***.
* If selected `object` is **not** type of *SOLIDWORKS Face* then üöÄ **[Else statement](/vba/if-then-structure-select-case/)** code runs.

When we select Model view, then selected object is *SOLIDWORKS Face*.

When we select from Feature Tree, then select object is *SOLIDWORKS Body Feature*.
{: .notice}

```vb
' Set the Solidworks Entity object to feature
Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
```

* When  üöÄ **[IF statement](/vba/if-then-structure-select-case/)** is **`True`** we run above line.
* In above line, we set the object inside array.
  * *Array of SOLIDWORKS Entities* = `swObjects`
  * *Indexed Entity Object* = `swObjects[selectItems]`
* **Indexed Entity Object** value set by `GetSelectedObject6()` method.
  * `GetSelectedObject6()` method is part of **SOLIDWORKS Selection Manager**.
  * **Reference**: Please visit üöÄ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr~GetSelectedObject6.html)**.

```vb
' Inform user to select a Face
MsgBox "Please select a Face."

' Clear selection
swDoc.ClearSelection2 True
```

* When üöÄ **[Else statement](/vba/if-then-structure-select-case/)** is **`True`** we execute run above lines.
* **1st Line**: We show a message to user to select **Feature** from *Feature Tree*.
* **2nd Line**: After that we clear our selection.

```vb
DoEvents
```

* After `For` loop end, we have a call for **`DoEvents`** function.
* This function repeats the `While` loop until we complete **Face** selection.

```vb
' Clear previous selection
swDoc.ClearSelection2 True
```

* In above line. we clear previous selection.

### Mark selected Entities

In previous section, we complete our selection.

In this section, we complete **Select and Mark entities** correctly our Face.

```vb
' Create Select data for this entity
Set swSelData = swSelMgr.CreateSelectData
```

* In above line, we create SOLIDWORKS Select Data for current SOLIDWORKS Entity object.

```vb
' For shell face, set mark to 1  
swSelData.Mark = 1
```

* In above lines, we set **Mark** to **`1`**.
* For ***Shell*** feature, **`Mark`** values will be **`1`**.

* **Reference**: Please visit üöÄ **[InsertFeatureShell Method](https://help.solidworks.com/2019/english/api/sldworksapi/solidworks.interop.sldworks~solidworks.interop.sldworks.imodeldoc2~insertfeatureshell.html)** in *SOLIDWORS API Help*.

```vb
' Select the current entity
swObject.Select4 True, swSelData
```

* In above line, we select the object.
* We select it by using `Select4` method of *SOLIDWORKS Entity object*.
* This method takes 2 arguments.
  * **Append** - *`True` appends the entity to the selection list, `False` replaces the selection list with this entity.*
  * **Data** - *Pointer to the **`ISelectData`** object.*
* We use following values as parameter.
  * **Append** - **`True`**
  * **Data** - **`swSelData`**

### Get unit Conversion factors

In this section we get unit Conversion factors.

```vb
' Local variables used as Conversion Factors
Dim LengthConversionFactor As Double
Dim AngleConversionFactor As Double

' Use a Select Case, to get the length of active Unit and set the different factors
Select Case swDoc.GetUnits(0)       ' GetUnits function gives us, active unit

Case swMETER    ' If length is in Meter
  LengthConversionFactor = 1
  AngleConversionFactor = 1

Case swMM       ' If length is in MM
  LengthConversionFactor = 1 / 1000
  AngleConversionFactor = 1 * 0.01745329

Case swCM       ' If length is in CM
  LengthConversionFactor = 1 / 100
  AngleConversionFactor = 1 * 0.01745329

Case swINCHES   ' If length is in INCHES
  LengthConversionFactor = 1 * 0.0254
  AngleConversionFactor = 1 * 0.01745329

Case swFEET     ' If length is in FEET
  LengthConversionFactor = 1 * (0.0254 * 12)
  AngleConversionFactor = 1 * 0.01745329

Case swFEETINCHES     ' If length is in FEET & INCHES
  LengthConversionFactor = 1 * 0.0254  ' For length we use sama as Inch
  AngleConversionFactor = 1 * 0.01745329

Case swANGSTROM        ' If length is in ANGSTROM
  LengthConversionFactor = 1 / 10000000000#
  AngleConversionFactor = 1 * 0.01745329

Case swNANOMETER       ' If length is in NANOMETER
  LengthConversionFactor = 1 / 1000000000
  AngleConversionFactor = 1 * 0.01745329

Case swMICRON       ' If length is in MICRON
  LengthConversionFactor = 1 / 1000000
  AngleConversionFactor = 1 * 0.01745329
End Select
```

* I have already explained about this in previous üöÄ **[Solidworks Macro - Fix Unit Issue](/solidworks-macros/unit-correction/)** article in this website.
* Please visit üöÄ **[Solidworks Macro - Fix Unit Issue](/solidworks-macros/unit-correction/)** article for more details.

### Get Shell Thickness

In this section, we get get the **Shell Thickness** from user.

```vb
' Variable to Shell thickness
Dim shellThickness As String
```

* In above line, we create a variable as a counter.
* **Variable Name**: `shellThickness`
* **Type**: `String`

```vb
' Getting Shell thickness from user
shellThickness = InputBox("Please select Shell Thickness.") * LengthConversionFactor
```

* In above line of code we are doing **3 steps** in one line.

Those 3 steps are explained below.

* **Step 1** - Getting **Shell Thickness** from user.
  Below image shows the message for **Shell Thickness** to the user.

[![message-to-select-shell-thickness](/assets/Solidworks_Images/feature-shell/message-to-enter-shell-thickness.png)](/assets/Solidworks_Images/feature-shell/message-to-enter-shell-thickness.png)

* **Step 2** - Convert input spacing as per document unit system.
* **Step 3** - Assigned converted value to **`shellThickness`** property.

### Create Shell feature

In this section, we create Shell feature.

```vb
' Create Shell feature
swDoc.InsertFeatureShell shellThickness, False
```

* In above line of code we create **Shel Feature** by `InsertFeatureShell` method.

* `InsertFeatureShell` method is part of `swDoc` variable i.e. `ModelDoc2` object.

* This `InsertFeatureShell` method takes following parameters as explained:

  - **Thickness** - *Shell thickness in meters*.
  - **Outward** - *`True` for outside, `False` for inside*.

* **Return Value** : This `InsertFeatureShell` method did not return any value.

* In our code, I have used following values:

  | Parameter Name | Value Used       |
  | -------------- | ---------------- |
  | **Thickness**  | `shellThickness` |
  | **Outward**    | `False`          |

* **Reference**: For more details about **InsertFeatureShell Method**: üöÄ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/solidworks.interop.sldworks~solidworks.interop.sldworks.imodeldoc2~insertfeatureshell.html)** for `InsertFeatureShell` Method.

[![shell-feature-final-result](/assets/Solidworks_Images/feature-shell/final-result-gif.gif)](/assets/Solidworks_Images/feature-shell/final-result-gif.gif)

### Final work

In this section, after creating **Shell Feature**, we have to do some cleaning work so that we can use this macro frequently.

```vb
' View zoom to fit
swDoc.ViewZoomtofit2
```

* In above line, we make our view zoom to fit the model.
* For this we use `ViewZoomtofit2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all previous selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **create Shell Feature** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
