---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Create Draft
permalink: /solidworks-vba-macros/create-draft/
tags:   [Solidworks Macro]
id: 'feature-create-draft'
---

{% include article-notes/intermediate-note.html %}

## Objective

In this article we **create and understand** VBA macro of **Draft feature** in *SOLIDWORKS CAD Software*.

This method is ***most updated*** method, so ***use this method*** if you want to create a new **Draft Feature** quickly. 

## Results We Can Get

After running our macro we successfully create **Draft feature** as a result.

Below image shows the result we get.

[![draft-feature-final-result](/assets/Solidworks_Images/feature-draft/final-result-gif.gif)](/assets/Solidworks_Images/feature-draft/final-result-gif.gif)

We create **Draft Feature** in following steps in general.

1. *Ask user to select **Neutral Face**.*
2. *Ask user to select **Faces to draft**.*
3. *Ask for **Draft Angle**.*

To get the correct result please follow the steps correctly.
{: .notice--warning}

## Macro Video

Below ðŸŽ¬ video shows **Draft feature** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/a6lhzUqVmoQ" frameborder="0" allowfullscreen></iframe>



***Above video is just for visualization and there are no explanation.*** 

***I have explained each and every line in this article.***

It is advisable to watch video, since it help you to better understand the process.
{: .notice--warning}

## VBA Macro

Below is the ***VBA macro*** for creating *Draft Feature*.

```vb
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Selection Manager
Dim swSelMgr As SldWorks.SelectionMgr

' Variable for Solidworks Entity
Dim swObject As SldWorks.Entity

' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData

' Variable for Solidworks Draft Feature
Dim swFeature As SldWorks.Feature

' Main program for Draft
Sub main()

  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Check if Solidworks is opened or not
  If swApp Is Nothing Then
    MsgBox ("Solidworks is not opened")
    Exit Sub
  End If
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox ("Solidworks document is not opened. Please open a document.")
    Exit Sub
  End If
  
  ' Set Solidworks Selection Manager variable
  Set swSelMgr = swDoc.SelectionManager
  
  ' Array of Solidworks Entities
  Dim swObjects(1 To 5) As SldWorks.Entity
  
  ' Local variable for selection
  Dim selectItems As Integer
  selectItems = 1
  
  ' Loop till we select all entities
  While selectItems <= 5
    
    ' Message to show user
    Dim messageToUser As String

    ' Update Messages
    Select Case selectItems
      Case 1
        messageToUser = "Please select a Neutral Face."
      Case 2, 3, 4, 5
        messageToUser = "Please select Faces for Draft."
    End Select
  
    ' Show message to user
    MsgBox messageToUser
      
    ' Loop until we complete our selection
    While swObjects(selectItems) Is Nothing
        
      ' Local integer for loop
      Dim i As Integer
      
      ' Looping until we select
      For i = 1 To swSelMgr.GetSelectedObjectCount2(-1)
        
        Select Case selectItems
          Case 1
            ' If the selection type is face
            If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then
                
                ' Set the Solidworks Entity object to feature
                Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
                
            Else
    
              ' Inform user to select a Face
              MsgBox "Please select a Face."
    
              ' Clear selection
              swDoc.ClearSelection2 True
            End If
          Case 2, 3, 4, 5
            ' If face is selected
            If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then
                
                Dim previousSelection As SldWorks.Entity
                Set previousSelection = swObjects(selectItems - 1)
                
                Dim currentSelection As SldWorks.Entity
                Set currentSelection = swSelMgr.GetSelectedObject6(i, -1)
                 
                If Not previousSelection Is currentSelection Then
                  ' Set the Solidworks Entity object to feature
                  Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
                End If
                
            Else
    
              ' Inform user to select a Face
              MsgBox "Please select a Face."
    
              ' Clear selection
              swDoc.ClearSelection2 True
            End If
        End Select
        
      Next
      
      DoEvents
    Wend
    
    ' Increase the selection count
    selectItems = selectItems + 1

  Wend
    
  ' Clear previous selection
  swDoc.ClearSelection2 True
  
  ' Local variable for counter
  Dim j As Integer
  j = 1
  
  ' Loop till counter is 6, since we have 5 selection
  While j < 6
    
    ' Set the current instance to Solidworks Entity variable
    Set swObject = swObjects(j)
    
    ' Create Select data for this entity
    Set swSelData = swSelMgr.CreateSelectData
    
    ' Update Marking as per selected entity
    Select Case j
      Case 1
        ' For neutral face, set mark to 1
        swSelData.Mark = 1
      Case 2, 3, 4, 5
        ' For draft faces, set mark to 2
        swSelData.Mark = 2
    End Select

    ' Select the current entity
    swObject.Select4 True, swSelData
    
    j = j + 1
      
  Wend
  
  ' Local variables used as Conversion Factors
  Dim LengthConversionFactor As Double
  Dim AngleConversionFactor As Double
  
  ' Use a Select Case, to get the length of active Unit and set the different factors
  Select Case swDoc.GetUnits(0)       ' GetUnits function gives us, active unit
    
    Case swMETER    ' If length is in Meter
      LengthConversionFactor = 1
      AngleConversionFactor = 1
    
    Case swMM       ' If length is in MM
      LengthConversionFactor = 1 / 1000
      AngleConversionFactor = 1 * 0.01745329
    
    Case swCM       ' If length is in CM
      LengthConversionFactor = 1 / 100
      AngleConversionFactor = 1 * 0.01745329
    
    Case swINCHES   ' If length is in INCHES
      LengthConversionFactor = 1 * 0.0254
      AngleConversionFactor = 1 * 0.01745329
    
    Case swFEET     ' If length is in FEET
      LengthConversionFactor = 1 * (0.0254 * 12)
      AngleConversionFactor = 1 * 0.01745329
    
    Case swFEETINCHES     ' If length is in FEET & INCHES
      LengthConversionFactor = 1 * 0.0254  ' For length we use sama as Inch
      AngleConversionFactor = 1 * 0.01745329
    
    Case swANGSTROM        ' If length is in ANGSTROM
      LengthConversionFactor = 1 / 10000000000#
      AngleConversionFactor = 1 * 0.01745329
    
    Case swNANOMETER       ' If length is in NANOMETER
      LengthConversionFactor = 1 / 1000000000
      AngleConversionFactor = 1 * 0.01745329
    
    Case swMICRON       ' If length is in MICRON
      LengthConversionFactor = 1 / 1000000
      AngleConversionFactor = 1 * 0.01745329
  End Select
  
  ' Variable to hold draft angle
  Dim response As String
  
  ' Getting draft angle from user.
  response = InputBox("Please Enter [Draft Angle]:", "Draft Fetaure")
  
  ' This will handle empty value or cancel case
  If Len(response) = 0 Then
    MsgBox "Empty or no value. Please try again."
    Exit Sub
  End If
  
  ' This will handle case for Non-numeric values
  If IsNumeric(response) = False Then
    MsgBox "Entered value is Non-numeric. Please try again."
    Exit Sub
  End If
  
  ' Variable to store validate draft angle
  Dim draftAngle As Double
  draftAngle = CDbl(response) * AngleConversionFactor
  
  ' This will handle case for 0 draft angle
  If draftAngle = 0 Then
    MsgBox "Entered value must be greater than 0. Please try again."
    Exit Sub
  End If
  
  ' Create Draft feature
  Set swFeature = swDoc.FeatureManager.InsertMultiFaceDraft(draftAngle, False, False, 0, False, False)

  ' Check if Draft feature creates or not
  If swFeature Is Nothing Then
    MsgBox ("Failed to create Draft feature.")
    Exit Sub
  End If
  
  ' Erase array data
  Erase swObjects
  
  ' View zoom to fit
  swDoc.ViewZoomtofit2
  
  ' Clear all selection
  swDoc.ClearSelection2 True

End Sub
```

## Prerequisite

There are some *prerequisite* for this article.

* Knowledge of **VBA programming language** is â—***required***.
* We are **not** creating feature from code but we use existing ðŸš€ **[Extrude feature](/solidworks-macros/create-extrude-feature)** to create **Draft feature**.

*Below image shown prerequisite 3D model for our demo.*

[![prerequisite](/assets/Solidworks_Images/feature-draft/prerequisite.png)](/assets/Solidworks_Images/feature-draft/prerequisite.png)

As shown in above image, there is ***one Extrude features*** in our part.

- `Base Feature` : This is our **Base Extrude feature**.


If you want to create this **Extrude** features ***programmatically*** then please refer to below article.

* ðŸš€ **[SOLIDWORKS Macros - Create Extrude/Boss](/solidworks-macros/create-extrude-feature)**

We will apply checks in this article, so the code we write should be error free most of the time.
{: .notice}

## Steps To Follow

This **Draft Feature VBA macro** can be divided into following sections:

1. *Creating Global Variables*
2. *Initializing required variables*
3. *Ask user to select faces*
4. *Mark selected entities*
5. *Get unit Conversion factors*
6. *Get Draft Angle And Validation*
7. *Create Draft feature*
8. *Final work*

***Every section with each line is explained below.***

I also give some ***links (see icon ðŸš€)*** so that you can go through them if there are anything I explained in previous articles.
{: .notice}

### Creating Global Variables

```vb
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: ðŸš€ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for Solidworks document. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb
' Variable for Solidworks Selection Manager
Dim swSelMgr As SldWorks.SelectionMgr
```

* **Purpose**:  In above line, we create a variable for Solidworks Selection Manager.
* **Variable Name**: `swSelMgr` 
* **Type**: `SldWorks.SelectionMgr`.
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr_members.html)**.

```vb
' Variable for Solidworks Entity
Dim swObject As SldWorks.Entity
```

* **Purpose**: In above line, we create a variable for *selection and marking* of Solidworks Entity.
* **Variable Name**: `swObject`
* **Type**: `SldWorks.Entity`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**.

```vb
' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData
```

* **Purpose**: In above line, we create a variable for **Marking** of Solidworks Select Data.
* **Variable Name**: `swSelData`
* **Type**: `SldWorks.SelectData`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectData_members.html)**.

```vb
' Variable for Solidworks Draft Feature
Dim swFeature As SldWorks.Feature
```

* **Purpose**: In above line, we create a variable for *Solidworks Draft Feature*.
* **Variable Name**: `swFeature`
* **Type**: `SldWorks.Feature`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeature_members.html)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb
' Main program for Draft
Sub main()

End Sub
```

* In above line, we create *main program for Draft Feature*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information ðŸš€ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

### Initializing Required Variables

Inside this section we initialize required variables.

```vb
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb
' Check if Solidworks is opened or not
If swApp Is Nothing Then  
  MsgBox ("SOLIDWORKS is not opened")  
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swApp`** variable.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swApp Is Nothing`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS is not opened*
  * Then we **stop** our macro here.

```vb
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened. Please open a document.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened. Please open a document.*
  * Then we **stop** our macro here.

```vb
' Set Solidworks Selection Manager variable
Set swSelMgr = swDoc.SelectionManager
```

* In above line, we set *value* of **`swSelMgr`** variable.
* This *value* is current document's *Selection Manager*.

```vb
' Array of Solidworks Entities
Dim swObjects(1 To 5) As SldWorks.Entity
```

* In above line, we create an ðŸš€ **[Array](/vba/arrays/)** of *Solidworks Entities*.
* **Variable Name**: `swObjects`
* **Type**: `SldWorks.Entity`
* **Length of Array**: *(1 To 5)*
* **Reference**: Please visit
  * **For Solidworks Entities**: ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**.
  * **For Arrays in VBA** : ðŸš€ **[Array on this website](/vba/arrays/)**.

```vb
' Local variable for selection
Dim selectItems As Integer
selectItems = 1
```

* In above line, we create a variable for selection and set its value.
* **Variable Name**: `selectItems`
* **Type**: `Integer`
* **Value**: **`1`**

### Ask user to select faces

In this section, we ask user to select **faces** for *Draft feature*.

```vb
' Loop till we select all entities
While selectItems <= 5

Wend
```

* In above line of code we start a `While` loop.
* We loop until `selectItems` variable's value is equal to **5**.
* **Reference**: ðŸš€ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Message to show user
Dim messageToUser As String
```

* In above line, we create a variable to show a messages we want to show before selection.
* **Variable Name**: `messageToUser`
* **Type**: `String`

```vb
' Update Messages
Select Case selectItems
Case 1
  messageToUser = "Please select a Neutral Face."
Case 2, 3, 4, 5
  messageToUser = "Please select Faces for Draft."
End Select
```

* In above code block, we use a `Select` statement to update message.
* We use the `case` on `selectItems`.
  * **Case 1**: `messageToUser` update to `"Please select a Neutral Face."`
  * **Case 2**: `messageToUser` update to `"Please select Faces for Draft."`
* **Reference**:  ðŸš€ **[Select statement](/vba/if-then-structure-select-case/#select-case-example)** article of this website.

```vb
' Show message to user
MsgBox messageToUser
```

* In above line, we show the message to user.
* Below image shows the message for **Neutral Face** to the user.

[![message-to-select-neutral-face](/assets/Solidworks_Images/feature-draft/message-to-select-neutral-face.png)](/assets/Solidworks_Images/feature-draft/message-to-select-neutral-face.png)

* Below image shows the message for **Faces for Draft** to the user.

[![message-to-select-draft-face](/assets/Solidworks_Images/feature-draft/message-to-select-draft-face.png)](/assets/Solidworks_Images/feature-draft/message-to-select-draft-face.png)

```vb
' Loop until we complete our selection
While swObjects(selectItems) Is Nothing    

Wend
```

* In above line, we create another `While` loop.
* This loops continues to run until we select the our **Feature** and **Edge** for circular pattern.
* **Reference**: ðŸš€ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Local integer for loop
Dim i As Integer
```

* In above line, we create a variable as a counter.
* **Variable Name**: `i`
* **Type**: `Integer`

```vb
' Loop until we select
For i = 1 To swSelMgr.GetSelectedObjectCount2(-1)

Next
```

* In above line, we create a `For` loop.
* This loops start from `i = 1` to number of `objects` we select.

```vb
' Update Messages
Select Case selectItems

End Select
```

* In above line, we use a `Select` statement for selection entities.
* We use the `case` on `selectItems`.
* **Reference**:  ðŸš€ **[Select statement](/vba/if-then-structure-select-case/#select-case-example)** article of this website.

```vb
Case 1
  ' If the selection type is face
  If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then

    ' Set the Solidworks Entity object to feature
    Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)

  Else

    ' Inform user to select a Face
    MsgBox "Please select a Face."

    ' Clear selection
    swDoc.ClearSelection2 True
  End If
```

* Above code block we use for **`Case 1`**.
* In above code block, we use  ðŸš€ **[IF/ElseIf statement](/vba/if-then-structure-select-case/)**  with conditions.
* The  ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)**  is `True` when selected `object` is type of ***SOLIDWORKS Face***.
* If selected `object` is **not** type of *SOLIDWORKS Face* then ðŸš€ **[Else statement](/vba/if-then-structure-select-case/)** code runs.

When we select the sketch from Model view, then selected object is *SOLIDWORKS Face*.

When we select the sketch from Feature Tree, then select object is *SOLIDWORKS Body Feature*.
{: .notice}

```vb
' Set the Solidworks Entity object to feature
Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
```

* When  ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** is **`True`** we run above line.
* In above line, we set the object inside array.
  * *Array of SOLIDWORKS Entities* = `swObjects`
  * *Indexed Entity Object* = `swObjects[selectItems]`
* **Indexed Entity Object** value set by `GetSelectedObject6()` method.
  * `GetSelectedObject6()` method is part of **SOLIDWORKS Selection Manager**.
  * **Reference**: Please visit ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr~GetSelectedObject6.html)**.

```vb
' Inform user to select a Face
MsgBox "Please select a Face."

' Clear selection
swDoc.ClearSelection2 True
```

* When ðŸš€ **[Else statement](/vba/if-then-structure-select-case/)** is **`True`** we execute run above lines.
* **1st Line**: We show a message to user to select **Feature** from *Feature Tree*.
* **2nd Line**: After that we clear our selection.

```vb
Case 2, 3, 4, 5
  ' If face is selected
  If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then

    Dim previousSelection As SldWorks.Entity
    Set previousSelection = swObjects(selectItems - 1)
    Dim currentSelection As SldWorks.Entity
    Set currentSelection = swSelMgr.GetSelectedObject6(i, -1)

    If Not previousSelection Is currentSelection Then
      ' Set the Solidworks Entity object to feature
      Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
    End If

  Else

    ' Inform user to select a Face
    MsgBox "Please select a Face."

    ' Clear selection
    swDoc.ClearSelection2 True
  End If
```

* Above code block we use for **`Case 2, 3, 4, 5`**.
* In above line, we use  ðŸš€ **[IF/Else statement](/vba/if-then-structure-select-case/)**  with *conditions*.
* ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)**  is `True` when selected `object` is type of *SOLIDWORKS Face*.
* If selected `object` is **not** type of *SOLIDWORKS Face* then ðŸš€ **[Else statement](/vba/if-then-structure-select-case/)** code runs.

```vb
Dim previousSelection As SldWorks.Entity
```

* **Purpose**: In above line, we create a variable for previously selected *Solidworks Entity*.
* **Variable Name**: `previousSelection`
* **Type**: `SldWorks.Entity` 

```vb
Set previousSelection = swObjects(selectItems - 1)
```

* In above line, we set the value of `previousSelection` variable to previously selected entity.

```vb
Dim currentSelection As SldWorks.Entity
```

* **Purpose**: In above line, we create a variable for currently selected *Solidworks Entity*.
* **Variable Name**: `currentSelection`
* **Type**: `SldWorks.Entity` 

```vb
Set currentSelection = swSelMgr.GetSelectedObject6(i, -1)
```

* In above line, we set the value of `currentSelection` variable to currently selected entity.

```vb
If Not previousSelection Is currentSelection Then

End If
```

* In above code, we check if previous and currently selected entities are different.
* So, we can select different faces.
* When ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** is `True` we execute code given below.

```vb
' Set the Solidworks Entity object to feature
Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
```

* In above line, we set the indexed object inside array.
  * *Array of SOLIDWORKS Entities* = `swObjects`
  * *Indexed Entity Object* = `swObjects[selectItems]`
* **Indexed Entity Object** value set by `GetSelectedObject6()` method.
  * `GetSelectedObject6()` method is part of **SOLIDWORKS Selection Manager**.
  * **Reference**: Please visit ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr~GetSelectedObject6.html)**.

```vb
' Inform user to select Face
MsgBox "Please select a Face."

' Clear selection
swDoc.ClearSelection2 True
```

* When ðŸš€ **[Else statement](/vba/if-then-structure-select-case/)** is `True` we execute code given below.
* **1st Line**: We show a message to user to select **Face**. 
* **2nd Line**: We clear our selection.

```vb
DoEvents
```

* After `For` loop end, we have a call for **`DoEvents`** function.
* This function repeats the `While` loop until we complete **Feature or Edge** selection.

```vb
' Clear previous selection
swDoc.ClearSelection2 True

' Increase the selection count
selectItems = selectItems + 1
```

* **1st Line**: We clear previous selection.
* **2nd Line**: Increment the **`selectItems`** count by 1.

After increment the selected count we continue our `while` loop.

### Mark selected Entities

In previous section, we complete our selection.

In this section, we complete **Select and Mark entities** correctly from our Array.

```vb
' Local variable for counter
Dim j As Integerj = 1
```

* In above line, we create a variable as a counter.
* **Variable Name**: `j`
* **Type**: `Integer`
* **Value**: **`1`**

```vb
' Loop till counter is 6, since we have 5 selection
While j < 6
  
Wend
```

* In above line, we create a `while` loop.
* **Condition**: that *counter value* of **`j`** should be *less than 6.*
* **Reference**: ðŸš€ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Set the current instance to Solidworks Entity variable
Set swObject = swObjects(j)
```

* In above line, we set the SOLIDWORKS Entity variable to current object from array.

```vb
' Create Select data for this entity
Set swSelData = swSelMgr.CreateSelectData
```

* In above line, we create SOLIDWORKS Select Data for current SOLIDWORKS Entity object.

```vb
' Update Marking as per selected entity
Select Case j

End Select
```

* In above line of code, we use a `Select` statement for selection entities.
* We use the `case` on `j`.
* **Reference**:  ðŸš€ **[Select statement](/vba/if-then-structure-select-case/#select-case-example)** article of this website.

```vb
Case 1  
	' For neutral face, set mark to 1  
	swSelData.Mark = 1
```

* Above code block, we use for **`Case 1`**.
* In above lines, we set **Mark** to **`1`**.
* For ***Draft*** feature, **`Mark`** values can be follows:

| Any...       | Must be preselected and marked with a value of... |
| ------------ | ------------------------------------------------- |
| Neutral face | 1                                                 |
| Draft faces  | 2                                                 |
| Draft Edges  | 4                                                 |

* **Reference**: Please visit ðŸš€ **[InsertMultiFaceDraft Method](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeatureManager~InsertMultiFaceDraft.html)** in *SOLIDWORS API Help*.

```vb
Case 2, 3, 4, 5  
	' For draft faces, set mark to 2  
	swSelData.Mark = 2
```

* Above code sample we use for **`Case 2, 3, 4, 5`**.
* In above lines, we update the **Mark** to **`2`**.
* For ***Draft*** feature, **`Mark`** values can be follows:

| Any...       | Must be preselected and marked with a value of... |
| ------------ | ------------------------------------------------- |
| Neutral face | 1                                                 |
| Draft faces  | 2                                                 |
| Draft Edges  | 4                                                 |

* **Reference**: Please visit ðŸš€ **[InsertMultiFaceDraft Method](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeatureManager~InsertMultiFaceDraft.html)** in *SOLIDWORS API Help*.

```vb
' Select the current entity
swObject.Select4 True, swSelData
```

* In above line, we select the object.
* We select it by using `Select4` method of *SOLIDWORKS Entity object*.
* This method takes 2 arguments.
  * **Append** - *`True` appends the entity to the selection list, `False` replaces the selection list with this entity.*
  * **Data** - *Pointer to the **`ISelectData`** object.*
* We use following values as parameter.
  * **Append** - **`True`**
  * **Data** - **`swSelData`**

```vb
j = j + 1
```

After **`Select`** statement, we increment the counter variable **`j`** by **1**.

### Get unit Conversion factors

In this section we get unit Conversion factors.

```vb
' Local variables used as Conversion Factors
Dim LengthConversionFactor As Double
Dim AngleConversionFactor As Double

' Use a Select Case, to get the length of active Unit and set the different factors
Select Case swDoc.GetUnits(0)       ' GetUnits function gives us, active unit

Case swMETER    ' If length is in Meter
  LengthConversionFactor = 1
  AngleConversionFactor = 1

Case swMM       ' If length is in MM
  LengthConversionFactor = 1 / 1000
  AngleConversionFactor = 1 * 0.01745329

Case swCM       ' If length is in CM
  LengthConversionFactor = 1 / 100
  AngleConversionFactor = 1 * 0.01745329

Case swINCHES   ' If length is in INCHES
  LengthConversionFactor = 1 * 0.0254
  AngleConversionFactor = 1 * 0.01745329

Case swFEET     ' If length is in FEET
  LengthConversionFactor = 1 * (0.0254 * 12)
  AngleConversionFactor = 1 * 0.01745329

Case swFEETINCHES     ' If length is in FEET & INCHES
  LengthConversionFactor = 1 * 0.0254  ' For length we use sama as Inch
  AngleConversionFactor = 1 * 0.01745329

Case swANGSTROM        ' If length is in ANGSTROM
  LengthConversionFactor = 1 / 10000000000#
  AngleConversionFactor = 1 * 0.01745329

Case swNANOMETER       ' If length is in NANOMETER
  LengthConversionFactor = 1 / 1000000000
  AngleConversionFactor = 1 * 0.01745329

Case swMICRON       ' If length is in MICRON
  LengthConversionFactor = 1 / 1000000
  AngleConversionFactor = 1 * 0.01745329
End Select
```

* I have already explained about this in previous ðŸš€ **[Solidworks Macro - Fix Unit Issue](/solidworks-macros/unit-correction/)** article in this website.
* Please visit ðŸš€ **[Solidworks Macro - Fix Unit Issue](/solidworks-macros/unit-correction/)** article for more details.

### Get Draft Angle And Validation

In this section, we get get the **Draft Angle** from user and apply some validation on **Draft Angle**.

```vb
' Variable to hold draft angle
Dim response As String
```

* In above line, we create a variable as a counter.
* **Variable Name**: `response`
* **Type**: `String`

```vb
' Getting draft angle from user
response = InputBox("Please Enter [Draft Angle]:", "Draft Fetaure")
```

* In above line of code we are doing **2 steps** in one line.

  Those 3 steps are explained below.

  * **Step 1** - Getting **Draft Angle** from user.

  Below image shows the message for **Draft Angle** to the user.

  [![message-to-enter-draft-angle](/assets/Solidworks_Images/feature-draft/message-to-enter-draft-angle.png)](/assets/Solidworks_Images/feature-draft/message-to-enter-draft-angle.png)

  * **Step 2** - Assigned input value to **`response`** variable.

```vb
' This will handle empty value or cancel case
If Len(response) = 0 Then  
  MsgBox "Empty or no value. Please try again."  
  Exit Sub
End If
```

* In above code block, we check the ***length of input value***.
* This check will handle **case for empty value** or **cancel operation case**.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`Len(response) = 0`**
  * **`Len()`** is pre-build VBA function which check the length of a object.
  * In above cases, we will get **0** value.
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Empty or no value. Please try again.*
  * Then we **stop** our macro here.

```vb
' This will handle case for Non-numeric values
If IsNumeric(response) = False Then  
  MsgBox "Entered value is Non-numeric. Please try again."  
  Exit Sub
End If
```

* In above code block, we check if the *input value* is **Non-numeric**.
* This check will handle **case for Non-numeric values**.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`IsNumeric(response) = False`**
  * **`IsNumeric()`** is pre-build VBA function which check if *passing object is **Numeric** or not*.
  * In above cases, we will get **False** value.
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Entered value is Non-numeric. Please try again.*
  * Then we **stop** our macro here.

```vb
' Variable to store validate draft angle
Dim draftAngle As Double
```

* In above line, we create a variable to store Rib Thickness.
* **Variable Name**: `draftAngle`
* **Type**: `Double`

```vb
draftAngle = CDbl(response) * AngleConversionFactor
```

* In above line of code we are doing **3 steps** in one line.

  Those 3 steps are explained below.

  * **Step 1** - Converting **Draft Angle** from user to **`Double`** type.
  * **Step 2** - Updating converted Rib Thickness as per document unit system.

  * **Step 3** - Assigned input value to **`draftAngle`** variable.

```vb
' This will handle case for 0 draft angle
If RibThickness = 0 Then  
  MsgBox "Entered value must be greater than 0. Please try again."  
  Exit Sub
End If
```

* In above code block, we check if the *input value* is **zero (0)**.
* This check will handle **case for 0 Draft Angle**.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`draftAngle = 0`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Entered value must be greater than 0. Please try again.*
  * Then we **stop** our macro here.

### Create Draft feature

In this section, we create Draft feature.

```vb
' Create Draft feature
Set swFeature = swDoc.FeatureManager.InsertMultiFaceDraft(draftAngle, False, False, 0, False, False)
```

* In above line, we set the value of variable `swFeature` by `InsertMultiFaceDraft` method.

* This `InsertMultiFaceDraft` method takes following parameters as explained:

  - **Angle** - *Draft angle in radians*.
  - **FlipDir** - *`True` to flip the direction of the draft*.
  - **EdgeDraft** - *`True` if this is an edge draft and the user selects edges to draft, `False` if this is a face draft and the user selects faces to draft.*
  - **PropType** - *Propagation type:*
    - **0** = *No propagation*
    - **1** = *Propagate to the next tangent faces that share the base neutral plane; valid for edge draft or neutral plane draft*
    - **2** = *Propagate to all faces that are a neighbor of the neutral plane*
    - **3** = *Propagate to all faces that a neighbor of the neutral plane on all inner loops*
    - **4** = *Propagate to all faces that are a neighbor of the neutral plane on the outer loop*
  - **IsStepDraft** - *`True` if the draft is a step draft, `False` to not*.
  - **IsBodyDraft** - *`True` if the draft is a body draft, `False` to not*.

* **Return Value** : This `InsertMultiFaceDraft` method return ðŸ‘‰ **[Feature](https://help.solidworks.com/2019/english/api/sldworksapi/SOLIDWORKS.Interop.sldworks~SOLIDWORKS.Interop.sldworks.IFeature.html)** data object.

* In our code, I have used following values:

  | Parameter Name  | Value Used   |
  | --------------- | ------------ |
  | **Angle**       | `draftAngle` |
  | **FlipDir**     | `False`      |
  | **EdgeDraft**   | `False`      |
  | **PropType**    | `0`          |
  | **IsStepDraft** | `False`      |
  | **IsBodyDraft** | `False`      |

**Reference**: For more details about 

* **Solidworks Feature Manager details**: ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeatureManager_members.html)** for Solidworks Feature Manager.
* **InsertMultiFaceDraft Method**: ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeatureManager~InsertMultiFaceDraft.html)** for `InsertMultiFaceDraft` Method.

```vb
' Check if Draft feature creates or not
If swFeature Is Nothing Then
  MsgBox ("Failed to create Draft feature.")
  Exit Sub
End If
```

* In above code block, we check if we successfully create **Draft Feature** or not.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swFeature Is Nothing`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Failed to create Draft Feature.*
  * Then we **stop** our macro here.

Now we run the macro and after running macro we get **Draft feature** as shown in below image.

[![draft-feature-final-result](/assets/Solidworks_Images/feature-draft/final-result-gif.gif)](/assets/Solidworks_Images/feature-draft/final-result-gif.gif)

### Final work

In this section, after creating **Draft Feature**, we have to do some cleaning work so that we can use this macro frequently.

```vb
' Erase array data
Erase swObjects
```

* In above line, we **erase** data from *SOLIDWORKS Entity array*.
* For this we use **`Erase`** function in-build in VBA.

```vb
' View zoom to fit
swDoc.ViewZoomtofit2
```

* In above line, we make our view zoom to fit the model.
* For this we use `ViewZoomtofit2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all previous selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **create Circular Pattern feature** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
