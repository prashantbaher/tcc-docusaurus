---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Toggle Mate Alignment
permalink: /solidworks-vba-macros/assembly-toggle-mate-alignment/
tags:   [Solidworks Macro]
id: 'assembly-toggle-mate-alignment'
---



## Objective

In this article, we understand "how to" **Toggle Mate Alignment** in **Assembly document** from VBA macro.

You can use this method to **Toggle Mate Alignment of any Mate**.

## Results We Can Get

Below image shows the result we get.

[![assembly-toggle-mate-alignment](/assets/Solidworks_Images/assembly-toggle-mate-alignment/final-result-gif.gif)](/assets/Solidworks_Images/assembly-toggle-mate-alignment/final-result-gif.gif)

We **Toggle Mate Alignment** in simple manners.

There are no extra steps required.

:::caution
To get the correct result, please follow the steps correctly.
:::

## Macro Video

Below üé¨ video shows how to **Toggle Mate Alignment** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/yyvAVI13mqk" frameborder="0" allowfullscreen 
width="100%"
height="500"></iframe>



***Above video is just for visualization and there is no explanation.*** 

***I have explained every line in this article.***

:::tip
It is advisable to watch video, since it helps you to better understand the process.
:::

## VBA Macro

Below is the ***VBA macro*** for *Toggle Mate Alignment*.

```vb showlinenumbers showLineNumbers
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc

' Variable for Solidworks Mate Feature
Dim swMateFeature As SldWorks.Feature

' Variable for Solidworks Mate Feature data
Dim swMateFeatureData As SldWorks.MateFeatureData

' Variable for Solidworks Distance Mate feature data
Dim swDistanceMateFeatureData As SldWorks.DistanceMateFeatureData

' Program to Toggle distance Mate Alignment
Sub main()

  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox "Solidworks document is not opened."
    Exit Sub
  End If
  
  ' Set Solidworks Assembly document
  Set swAssembly = swDoc
  
  ' Get mate feature
  Set swMateFeature = swDoc.Extension.GetLastFeatureAdded
  
  ' Check if successfully Get mate
  If swMateFeature Is Nothing Then
    MsgBox "Failed to Get Mate."
    swDoc.ClearSelection2 True
    Exit Sub
  End If
  
  ' Select the mate
  swMateFeature.Select True
  
  ' Start editing mate feature
  swDoc.FeatEdit
  
  ' Get feature definition of mate
  Set swMateFeatureData = swMateFeature.GetDefinition
  
  ' Set Distance mate feature data
  Set swDistanceMateFeatureData = swMateFeatureData
  
  ' Print Mate Alignment
  Debug.Print swDistanceMateFeatureData.MateAlignment
  
  ' Toggle distance Mate Alignment
  If swDistanceMateFeatureData.MateAlignment = 1 Then
    swDistanceMateFeatureData.MateAlignment = 2
  Else
    swDistanceMateFeatureData.MateAlignment = 1
  End If
  
  ' Print updated Mate Alignment
  Debug.Print swDistanceMateFeatureData.MateAlignment
  
  ' Modify the definition
  swMateFeature.ModifyDefinition swDistanceMateFeatureData, swDoc, Nothing
  
  ' Clear all selection
  swDoc.ClearSelection2 True
  
  ' Rebuild assembly
  swDoc.ForceRebuild3 True
  
End Sub
```

## Prerequisite

There are some *prerequisites* for this article.

* Knowledge of **VBA programming language** is ‚ùó***required***.

* We use existing parts in Assembly document.

* Both components are fully constraint as shown in below image.

[![prerequisite](/assets/Solidworks_Images/assembly-toggle-mate-alignment/prerequisite.gif)](/assets/Solidworks_Images/assembly-toggle-mate-alignment/prerequisite.gif)

:::note
We will apply checks in this article, so the code we write, should be **error free** mostly.
:::

## Steps To Follow

This **VBA macro** can be divided into following sections:

1. *Create Global Variables*
2. *Initialize Global Variables*
3. *Get Feature Data*
4. *Toggle Mate Alignment*

***Every section with each line is explained below.***

:::tip
I also give some ***links (see icon üöÄ)*** so that you can go through them if there are anything I explained in previous articles.
:::

### Create Global Variables

In this section, we create global variables.

```vb showlinenumbers showLineNumbers
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: üöÄ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for *Solidworks document*. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc
```

* **Purpose**: In above line, we create a variable for *Solidworks Assembly*.
* **Variable Name**: `swAssembly`
* **Type**: `SldWorks.AssemblyDoc`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Mate Feature
Dim swMateFeature As SldWorks.Feature
```

* **Purpose**: In above line, we create a variable for *Solidworks Mate Feature*.
* **Variable Name**: `swMateFeature` 
* **Type**: `SldWorks.Feature`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeature_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Mate Feature data
Dim swMateFeatureData As SldWorks.MateFeatureData
```

* **Purpose**: In above line, we create a variable for *Solidworks Mate Feature Data*.
* **Variable Name**: `swMateFeatureData` 
* **Type**: `SldWorks.MateFeatureData`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IMateFeatureData_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Distance Mate feature data
Dim swDistanceMateFeatureData As SldWorks.DistanceMateFeatureData
```

* **Purpose**: In above line, we create a variable for *Solidworks Symmetric Mate Feature Data*.
* **Variable Name**: `swDistanceMateFeatureData` 
* **Type**: `SldWorks.DistanceMateFeatureData `.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IDistanceMateFeatureData.html)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb showlinenumbers showLineNumbers
' Program to Toggle distance Mate Alignment
Sub main()

End Sub
```

* In above line, we create *main Program to Toggle Distance Mate in assembly*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

### Initialize Global Variables

In this section, we initialize global variables.

```vb showlinenumbers showLineNumbers
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb showlinenumbers showLineNumbers
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb showlinenumbers showLineNumbers
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened.*
  * Then we **stop** our macro here.

```vb showlinenumbers showLineNumbers
' Set Solidworks Assembly document
Set swAssembly = swDoc
```

* In above line, we set *value* of **`swAssembly`** variable.
* This *value* is **`swDoc`** variable.

### Get Feature Data

In this section, we *Get Feature Data*.

```vb showlinenumbers showLineNumbers
' Get mate feature
Set swMateFeature = swDoc.Extension.GetLastFeatureAdded
```

* In above line, we set the value of variable **`swMateFeature`** by **`GetLastFeatureAdded`** method.
* This **`GetLastFeatureAdded`** method is part of **`Extension`** object.
* This **`Extension`** object is then part of **`swDoc`** object.
* **`GetLastFeatureAdded`** method gives us last added mate.

```vb showlinenumbers showLineNumbers
' Check if successfully Get mate
If swMateFeature Is Nothing Then
  MsgBox "Failed to Get Mate."
  swDoc.ClearSelection2 True
  Exit Sub
End If
```

* In above code block, we check if we successfully get **Last Mate** or not.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swMateFeature Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Failed to Get Mate*.
  * After that we clear the selection.
  * Then we **stop** our macro here.

```vb showlinenumbers showLineNumbers
' Select the mate
swMateFeature.Select True
```

* In above line we select the mate by **`Select`** method.
* This **`Select`** method take either **`True`** or **`False`**.
  * **`True`**: *Appends the feature to the current selection list.*
  * **`False`**: *Replaces the current selection list.*

```vb showlinenumbers showLineNumbers
' Start editing mate feature
swDoc.FeatEdit
```

* In above line we start editing mate feature by **`FeatEdit`** method.
* This **`FeatEdit`** method puts the current feature into edit mode. 

```vb showlinenumbers showLineNumbers
' Get feature definition of mate
Set swMateFeatureData = swMateFeature.GetDefinition
```

* In above line, we get the **feature definition** of *select distance mate*.
* We get this feature definition by **`GetDefinition`** method.
* This **`GetDefinition`** method is part of **`SldWorks.Feature`** object.

```vb showlinenumbers showLineNumbers
' Set Distance mate feature data
Set swDistanceMateFeatureData = swMateFeatureData
```

* In above line, we set **Distance mate feature data** to feature definition of *distance mate*.
* We set **value** of variable **`swDistanceMateFeatureData`** to variable **`swMateFeatureData`**.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IDistanceMateFeatureData_members.html)**.

### Toggle Mate Alignment

In this section, we *Toggle Mate Alignment*.

```vb showlinenumbers showLineNumbers
' Print Mate Alignment
Debug.Print swDistanceMateFeatureData.MateAlignment
```

* In above code block, *we print selected mate's current Alignment*.
* For this we set the value **`MateAlignment`** property of **`swDistanceMateFeatureData`** variable.
* We get the value of **`MateAlignment`** property to **`newDistance`** variable.

```vb showlinenumbers showLineNumbers
' Toggle distance Mate Alignment
If swDistanceMateFeatureData.MateAlignment = 1 Then
  swDistanceMateFeatureData.MateAlignment = 2
Else
  swDistanceMateFeatureData.MateAlignment = 1
End If
```

* In above code block, we check *Mate Alignment*.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDistanceMateFeatureData.MateAlignment = 1`**
* When this condition is **`True`**, 
  * We set the **`Mate Alignment`** value to **`2`**.
* When this condition is **`False`**, 
  * We set the **`Mate Alignment`** value to **`1`**.

```vb showlinenumbers showLineNumbers
' Print updated Mate Alignment
Debug.Print swDistanceMateFeatureData.MateAlignment
```

* In above code block, *we print selected mate's current Alignment*.
* For this we set the value **`MateAlignment`** property of **`swDistanceMateFeatureData`** variable.
* We get the value of **`MateAlignment`** property to **`newDistance`** variable.

```vb showlinenumbers showLineNumbers
' Modify the definition
swMateFeature.ModifyDefinition swDistanceMateFeatureData, swDoc, Nothing
```

* In above line, we modify the definition of selected distance mate.
* We use **`ModifyDefinition`** method to update definition.
* This **`ModifyDefinition`** method is part of **`SldWorks.Feature`** object.
* This **`ModifyDefinition`** method takes 3 parameters as follows:

  * **Data**: *Feature data object*
  * **TopDoc**: *Top-level document*
  * **Component**: *Component for the feature* 

* **Reference**: For more details please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeature~ModifyDefinition.html)**.


* **Return Value** : This **`ModifyDefinition`** method return **`True`** if the feature definition modified successfully, **`False`** if not.

* In our code, I have used following values:

  | Parameter Name  | Value Used   |
  | --------------- | ------------ |
  | **Data**        | `swDistanceMateFeatureData` |
  | **TopDoc**      | `swDoc` |
  | **Component**   | `Nothing` |

```vb showlinenumbers showLineNumbers
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb showlinenumbers showLineNumbers
' Rebuild assembly
swDoc.ForceRebuild3 True
```

* In above line, we Rebuild assembly.
* For this we use `ForceRebuild3` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

Now we run the macro and after running macro we get **Modified Distance Mate** as shown in below image.

[![assembly-toggle-mate-alignment](/assets/Solidworks_Images/assembly-toggle-mate-alignment/final-result-gif.gif)](/assets/Solidworks_Images/assembly-toggle-mate-alignment/final-result-gif.gif)

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **Rename Mate or any Feature** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
