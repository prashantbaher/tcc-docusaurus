---
categories: Solidworks-macro
title:  Solidworks Macro - Create Polygon
permalink: /solidworks-macros/polygon/
tags:   [Solidworks Macro]
id: 'create-polygon'
---

In this post, I tell you about *how to create a Polygon through Solidworks VBA Macros* in a sketch.

The process is almost identical with previous **[Sketch - Create Tangent Arc](/solidworks-macros/create-tangent-arc)** post.

In this post, I tell you about `CreatePolygon` method from **Solidworks** `SketchManager` object.

This method is ***most updated*** method, I found in *Solidworks API Help*. 

So ***use this method*** if you want to create a new **Polygon**.

---

## Video of Code on YouTube

Please see below video on **how to create a Polygon** from Solidworks VBA Macros.

<iframe src="https://www.youtube.com/embed/4hsvqMJmz1E" frameborder="0" allowfullscreen></iframe>

Please note that there are **no explaination** in the video. 

**Explaination** of each line and why we write code this way is given in this post.

---

## Code Sample

Below is the `code` sample for creating *a Polygon*.

```vb
Option Explicit

' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
' Boolean Variable
Dim BoolStatus As Boolean
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager

' Main function of our VBA program
Sub main()

  ' Setting Solidworks variable to Solidworks application
  Set swApp = Application.SldWorks
  
  ' Creating string type variable for storing default part location
  Dim defaultTemplate As String
  ' Setting value of this string type variable to "Default part template"
  defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)

  ' Setting Solidworks document to new part document
  Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)

  ' Selecting Front Plane
  BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
  
  ' Setting Sketch manager for our sketch
  Set swSketchManager = swDoc.SketchManager
  
  ' Inserting a sketch into selected plane
  swSketchManager.InsertSketch True
  
  ' Creating Varient for Polygon
  Dim myPolygon As Variant
  
  ' Creating a Polygon
  myPolygon = swSketchManager.CreatePolygon(0, 0, 0, 1, 0, 0, 6, True)
  
  ' De-select the Polygon after creation
  swDoc.ClearSelection2 True
  
  ' Zoom to fit screen in Solidworks Window
  swDoc.ViewZoomtofit

End Sub
```

---

## Understanding the Code

Now let us walk through *each line* in the above code, and **understand** the meaning of every line.

```vb
Option Explicit
```

This line forces us to define every variable we are going to use. 

For more information please visit **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.

```vb
' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

In this line, we are creating a variable which we named as `swApp` and the type of this `swApp` variable is `SldWorks.SldWorks`.

```vb
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

In this line, we are creating a variable which we named as `swDoc` and the type of this `swDoc` variable is `SldWorks.ModelDoc2`.

Next is our `Sub` procedure named as `main`. This procedure hold all the *statements (instructions)* we give to computer.

```vb
' Setting Solidworks variable to Solidworks application
Set swApp = Application.SldWorks
```

In this line, we are setting the value of our Solidworks variable `swApp` which we defined earlier to Solidworks application.

```vb
' Creating string type variable for storing default part location
Dim defaultTemplate As String
' Setting value of this string type variable to "Default part template"
defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)
```

In 1st statement of above example, we are defining a variable of `string` type and named it as `defaultTemplate`.

This variable `defaultTemplate`, holds the location the location of **Default Part Template**.

In 2nd line of above example. we assign value to our newly define `defaultTemplate` variable.

We assign the value by using a *Method* named `GetUserPreferenceStringValue()`. 

This method is a part of our main Solidworks variable `swApp`.

```vb
' Setting Solidworks document to new part document
Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)
```

In this line, we set the value of our `swDoc` variable to new document.

For **more detailed information** about above lines please visit **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post. 

I have discussed them **thoroghly** in **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post, so do checkout this post if you don't understand above code.

```vb
' Boolean Variable
Dim BoolStatus As Boolean

' Selecting Front Plane
BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
```

In 1st line, we create a variable named `BoolStatus` as `Boolean` object.

In next line, we select the *front plane* by using `SelectByID2` method from `Extension` object.

For more information about selection method please visit **[Solidworks Macros - Selection Methods](/solidworks-macros/select-plane-from-tree)** post.

I have discussed about different *Selection methods* in details in **[Soldworks Macros - Selection Methods](/solidworks-macros/select-plane-from-tree)** post, so do visit this post for more *Selection methods*.

```vb
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager
```

In above line, we create variable `swSketchManager` for **Solidworks Sketch Manager**.

As the name suggested, a **Sketch Manager** holds variours methods and properties to manage *Sketches*.

To see methods and properties related to SketchManager object, please visit **[this page of Solidworks API Help](https://help.solidworks.com/2017/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchManager_members.html)**

```vb
' Setting Sketch manager for our sketch
Set swSketchManager = swDoc.SketchManager
```

In above line, we set the **Sketch manager** variable to current document's sketch manager.

```vb
' Inserting a sketch into selected plane
swSketchManager.InsertSketch True
```

In above line, we use `InsertSketch` method of *SketchManager* and give `True` value.

This method allows us to insert a sketch in selected plane.

```vb
' Creating Varient for Polygon
Dim myPolygon As Variant

' Creating a Polygon
myPolygon = swSketchManager.CreatePolygon(0, 0, 0, 1, 0, 0, 6, True)
```

In above sample code, we 1st create a variable named `myPolygon` of type `Variant`.

In 2nd line, we **get** the value of *Variant* variable `myPolygon`.

We get this value from `CreatePolygon` method which is inside the `swSketchManager` variable.

`swSketchManager` variable is a type of **SketchManager**, hence we used `CreatePolygon` method from **SketchManager**.

This `CreatePolygon` method takes following parameters as explained:

- *XC* : X coordinate of the center point, of the polygon

- *YC* : Y coordinate of the center point, of the polygon

- *ZC* : Z coordinate of the center point, of the polygon

- *Xp* : X coordinate of the vertex point, of the polygon

- *Yp* : Y coordinate of the vertex point, of the polygon

- *Zp* : Z coordinate of the vertex point, of the polygon

- *Sides* : Number of sides in the polygon

- *Inscribed* : `True` to show an inscribed construction circle, `False` to show a circumscribed construction circle

For creating a *Polygon*, I used (0, 0, 0) as the *Center point*, which is *origin point* of our Sketch.

For *Vertex point* of *Polygon*, I used (1, 0, 0) which is 1 point distance in X-direction.

For *Number of sides* in the *Polygon*, I used *6* which represent **a Hexagon**.

Below Image show when *Inscribed* option is `True` and show an *inscribed construction circle*.

![polygon-inscribed-circle-true](/assets/Solidworks_Images/arcs/polygon-inscribed-circle-true.png)

Below Image show when *Inscribed* option is `False`and show a *circumscribed construction circle*.

![polygon-inscribed-circle-false](/assets/Solidworks_Images/arcs/polygon-inscribed-circle-false.png)

This `CreatePolygon` method returns **an array** of *sketch segments* that represent the sides created for this *Polygon*.

A *Sketch Segment* can represent a sketch arc, line, ellipse, parabola or spline.

Sketch Segment has `ISketchSegment` Interface, which provides functions that are generic to every type of sketch segment.

For example, every sketch segment has an ID and can be programmatically selected.

Therefore, the `ISketchSegment` interface provides functions to obtain the ID and to select the item.

### NOTE

It is ***very important*** to remember that, when you give distance or any other numeric value in **Solidworks API**, Solidworks takes that numeric value in ***Meter only***.

*Solidworks API* does not care about your application's Unit systems.

For example, I works in ANSI system means "inches" for distance. 

But when I used Solidworks API through *VBA macros* or *C#*, I have to use **converted** numeric values.

Because Solidworks API output the distance in **Meter** only; which is not my requirement.

```vb
' De-select the Polygon after creation
swDoc.ClearSelection2 True
```

In the this line of code, we de-select the created Polygon.

For de-selecting, we use `ClearSelection2` method from our Solidworks document variable `swDoc`.

```vb
' Zoom to fit screen in Solidworks Window
swDoc.ViewZoomtofit
```

In this last line we use *zoom to fit* command.

For Zoom to fit, we use `ViewZoomtofit` method from our Solidworks document variable `swDoc`. 

Hope this post helps you to *create a Polygon* in Sketches with Solidworks VB Macros.

For more such tutorials on **Solidworks VBA Macros**, do come to this blog after sometime.

Till then, Happy learning!!!

