---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Add Symmetric Mate
permalink: /solidworks-vba-macros/assembly-symmetric-mate/
tags:   [Solidworks Macro]
id: 'assembly-symmetric-mate'
---

import AdComponent from '@site/src/components/Adsense';

## Objective

<AdComponent />

In this article we **create and understand** VBA macro of adding **Symmetric Mate** in **Assembly document**.

This method is ***most updated*** method, so ***use this method*** if you want to add **Symmetric Mate**. 

## Results We Can Get

After running our macro, we successfully add **Symmetric Mate** as a result.

Below image shows the result we get.

[![assembly-symmetric-mate](/assets/Solidworks_Images/assembly-symmetric-mate/final-result-gif.gif)](/assets/Solidworks_Images/assembly-symmetric-mate/final-result-gif.gif)

We add **Symmetric Mate** in following steps.

1. *loop through each component in current assembly.*
2. *Loop through each face.*
3. *Get desired face.*
4. *Add Symmetric Mate.*
5. *Final Work*

:::caution
To get the correct result, please follow the steps correctly.
:::

## Macro Video

Below üé¨ video shows **Adding Symmetric Mate** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/K0JJ_sN_Tp0" frameborder="0" allowfullscreen 
width="100%"
height="500"></iframe>



***Above video is just for visualization and there is no explanation.*** 

***I have explained every line in this article.***

:::tip
It is advisable to watch video, since it helps you to better understand the process.
:::

## VBA Macro

Below is the ***VBA macro*** for *Symmetric Mate*.

```vb showlinenumbers showLineNumbers
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc

' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2

' Variable for Solidworks Body
Dim swBody As SldWorks.Body2

' Variable for Solidworks Mate Feature
Dim swMateFeature As SldWorks.Feature

' Variable for Solidworks Face
Dim swFace As SldWorks.Face2

' Variable for Solidworks Face List
Dim vFaces(1 To 2) As SldWorks.Face2

' Variable for Solidworks Mate Feature Data
Dim swMateData As SldWorks.MateFeatureData

' Variable for Solidworks Symmetric Mate Feature Data
Dim swSymmetricMateData As SldWorks.SymmetricMateFeatureData

' Program to add Symmetric Mate
Sub main()
  
  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox "Solidworks document is not opened."
    Exit Sub
  End If
  
  ' Set Solidworks Assembly document
  Set swAssembly = swDoc
    
  ' Variable for List of elements
  Dim vArray As Variant
  
  ' Get Components list in opened assembly
  vArray = swAssembly.GetComponents(True)
  
  ' Variable for component Index
  Dim componentIndex As Integer
  
  ' Loop Components List
  For componentIndex = 0 To UBound(vArray)
    
    ' Set Solidworks Component variable
    Set swComponent = vArray(componentIndex)
    
    ' Calling this function
    GetFaces swComponent, componentIndex
    
    ' If we already seleted our faces
    If Not vFaces(2) Is Nothing Then
      Exit For  ' Break the loop
    End If
    
  Next
  
  ' Create Assembly Mate Data
  Set swMateData = swAssembly.CreateMateData(swMateType_e.swMateSYMMETRIC)
  
  ' Set Symmetric Mate data to Assembly Mate Data
  Set swSymmetricMateData = swMateData
  
  ' Set Entities to Mate
  swSymmetricMateData.EntitiesToMate = vFaces
  
  ' Set Symmetric Mate faces
  swSymmetricMateData.SymmetryPlane = swAssembly.FeatureByName("SYMMETRY PLANE")
  
  ' Add Symmetric Mate
  Set swMateFeature = swAssembly.CreateMate(swSymmetricMateData)

  ' Check if Mate is added or not
  If swMateFeature Is Nothing Then
    MsgBox "Failed to Add Mate."
    swDoc.ClearSelection2 True
    Exit Sub
  End If
  
  ' Empty Face Array
  Set vFaces(1) = Nothing
  Set vFaces(2) = Nothing
  
  ' Clear all selection
  swDoc.ClearSelection2 True
  
  ' Zoom view to fit
  swDoc.ViewZoomtofit2
  
  ' Rebuild assembly
  swDoc.ForceRebuild3 True
  
End Sub

' Function to get required face for Symmetric mate
Function GetFaces(component As SldWorks.Component2, componentIndex As Integer)
  
  ' Get body of current component
  Set swBody = component.GetBody
  
  ' Get First face
  Set swFace = swBody.GetFirstFace
  
  ' Variable to store Message Box result
  Dim resp As VbMsgBoxResult
  
  ' Variable to count added faces
  Dim faceNumber As Integer: faceNumber = 0
  
  ' Loop through all Faces
  Do While Not swFace Is Nothing
    
    ' Clear selection
    swDoc.ClearSelection2 True
    
    ' Select current face
    swFace.Select True
    
    ' Ask user to confirm this selection
    resp = MsgBox("Is this correct Face?", vbYesNo, "Select Face")
    
    ' Check if user response if Yes
    If resp = vbYes Then
      ' Add current face to List of Faces
      Set vFaces(faceNumber + 1) = swFace
      
      ' Increment face count
      faceNumber = faceNumber + 1
    End If
    
    ' When both faces are added
    If faceNumber = 2 Then
      Exit Function
    End If
    
    ' Get next face
    Set swFace = swFace.GetNextFace
    
  Loop
  
End Function
```

<AdComponent />

## Prerequisite

There are some *prerequisites* for this article.

* Knowledge of **VBA programming language** is ‚ùó***required***.

* We use existing parts in Assembly document.

* One component is fully constraint and other component is Float as shown in below image.

[![prerequisite](/assets/Solidworks_Images/assembly-symmetric-mate/prerequisite.gif)](/assets/Solidworks_Images/assembly-symmetric-mate/prerequisite.gif)

:::note
We will apply checks in this article, so the code we write, should be **error free**.
:::

## Steps To Follow

This **VBA macro** can be divided into following sections:

1. *Create and initialize required variables*
2. *Get components and Loop through them*
3. *Get desired faces*
4. *Add Symmetric Mate*
5. *Final Work*

***Every section with each line is explained below.***

:::tip
I also give some ***links (see icon üöÄ)*** so that you can go through them if there are anything I explained in previous articles.
:::

### Create and Initialize required variables

```vb showlinenumbers showLineNumbers
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: üöÄ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for *Solidworks document*. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc
```

* **Purpose**: In above line, we create a variable for *Solidworks Assembly*.
* **Variable Name**: `swAssembly`
* **Type**: `SldWorks.AssemblyDoc`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2
```

* **Purpose**: In above line, we create a variable for *Solidworks Component*.
* **Variable Name**: `swComponent` 
* **Type**: `SldWorks.Component2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IComponent2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Body
Dim swBody As SldWorks.Body2
```

* **Purpose**: In above line, we create a variable for *Solidworks Component*.
* **Variable Name**: `swBody` 
* **Type**: `SldWorks.Body2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IBody2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Mate Feature
Dim swMateFeature As SldWorks.Feature
```

* **Purpose**: In above line, we create a variable for *Solidworks Mate Feature*.
* **Variable Name**: `swMateFeature` 
* **Type**: `SldWorks.Feature`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeature_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Face
Dim swFace As SldWorks.Face2
```

* **Purpose**: In above line, we create a variable for *Solidworks Face*.
* **Variable Name**: `swFace` 
* **Type**: `SldWorks.Face2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFace2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Face List
Dim vFaces(1 To 2) As SldWorks.Face2
```

* **Purpose**: Define üöÄ **[Array](/vba/vba-arrays/)** of *Face2*.
* **Variable Name**: `vFaces`
* **Type**: `Face2`
* **Length of Array**: *(1 To 2)*
* **Reference**: Please visit
  * **For Arrays in VBA** : üöÄ **[Array on this website](/vba/vba-arrays/)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Mate Feature Data
Dim swMateData As SldWorks.MateFeatureData
```

* **Purpose**: In above line, we create a variable for *Solidworks Mate Feature Data*.
* **Variable Name**: `swMateData` 
* **Type**: `SldWorks.MateFeatureData`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IMateFeatureData_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Symmetric Mate Feature Data
Dim swSymmetricMateData As SldWorks.SymmetricMateFeatureData
```

* **Purpose**: In above line, we create a variable for *Solidworks Symmetric Mate Feature Data*.
* **Variable Name**: `swAngleMateData` 
* **Type**: `SldWorks.AngleMateFeatureData`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAngleMateFeatureData_members.html)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb showlinenumbers showLineNumbers
' Program to add Symmetric Mate
Sub main()

End Sub
```

* In above line, we create *main Program to add Symmetric Mate in assembly*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/vba-sub-and-function-procedure/)** article of this website.

```vb showlinenumbers showLineNumbers
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb showlinenumbers showLineNumbers
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb showlinenumbers showLineNumbers
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use üöÄ **[IF statement](/vba/vba-if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/vba-msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened.*
  * Then we **stop** our macro here.

```vb showlinenumbers showLineNumbers
' Set Solidworks Assembly document
Set swAssembly = swDoc
```

* In above line, we set *value* of **`swAssembly`** variable.
* This *value* is **`swDoc`** variable.

### Get Components and Loop through them

In this section, we *Get Components and Loop through them*.

```vb showlinenumbers showLineNumbers
' Variable for List of elements
Dim vArray As Variant
```

* **Purpose**: In above line, we create a variable for *List of elements*.
* **Variable Name**: `vArray`
* **Type**: `Variant`

```vb showlinenumbers showLineNumbers
' Get Components list in opened assembly
vArray = swAssembly.GetComponents(True)
```

* In above line, we set the value of **`vArray`** variable. 
* We set value by **`GetComponents`** method of **`swAssembly`** variable.

```vb showlinenumbers showLineNumbers
' Variable for component Index
Dim componentIndex As Integer
```

* In above line, we create a variable for component Index as a counter.
* **Variable Name**: `componentIndex`
* **Type**: `Integer`

```vb showlinenumbers showLineNumbers
' Loop Components List
For componentIndex = 0 To UBound(vArray)
  
Next
```

* In above line, we create a `For` loop.
* This loops start from `i = 0` to maximum number of items in `vArray` we select.

```vb showlinenumbers showLineNumbers
' Set Solidworks Component variable
Set swComponent = vArray(componentIndex)
```

* In above line, we set *value* of **`swComponent`** variable.
* This *value* is current value of array **`vArray`**.

<AdComponent />

### Get desired face

In this section we get desired face for **Symmetric mate**.

```vb showlinenumbers showLineNumbers
' Calling this function
GetFaces swComponent, componentIndex
```

* In above line, we call a function.
* **Function Name**: **`GetFaces`**
* **Function Parameters**: They are
  * **`swComponent`**
  * **`componentIndex`**

```vb showlinenumbers showLineNumbers
' Function to select required face for Symmetric mate
Function GetFaces(component As SldWorks.Component2, componentIndex As Integer)
  
End Function
```

* In above line, we create a **Function** to select required face for Symmetric mate.
* This is a **`Function`** procedure which has name of **`GetFaces`**. 
* This procedure hold all the *statements (instructions)* for *select required face for Symmetric mate*.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/vba-sub-and-function-procedure/)** article of this website.

This function has 2 parameters as follows:
* **`component As SldWorks.Component2`**
* **`componentIndex As Integer`**

```vb showlinenumbers showLineNumbers
' Get body of current component
Set swBody = component.GetBody
```

* In above line, we set *value* of **`swBody`** variable.
* Value of **`swBody`** variable is set by **`GetBody`** method of **`component`** variable.

```vb showlinenumbers showLineNumbers
' Get First face
Set swFace = swBody.GetFirstFace
```

* In above line, we set *value* of **`swFace`** variable.
* Value of **`swFace`** variable is set by **`GetFirstFace`**, which is part of **`swBody`** variable.

```vb showlinenumbers showLineNumbers
' Variable to store Message Box result
Dim resp As VbMsgBoxResult
```

* **Purpose**: In above line, we create a variable to store Message Box result.
* **Variable Name**: `resp`
* **Type**: `VbMsgBoxResult`

```vb showlinenumbers showLineNumbers
' Variable to count added faces
Dim faceNumber As Integer: faceNumber = 0
```

* **Purpose**: In above line, we create a variable to count number of faces added.
  * At the same time we set the **value** of this variable `faceNumber` to `0` i.e. `faceNumber = 0`
* **Variable Name**: `faceNumber`
* **Type**: `Integer`

```vb showlinenumbers showLineNumbers
' Loop through all Faces
Do While Not swFace Is Nothing
  
Loop
```

* In above line, we loop through all Faces.

```vb showlinenumbers showLineNumbers
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb showlinenumbers showLineNumbers
' Select current face
swFace.Select True
```

* In above line, we select current face.

:::info
This function is not available in üöÄ **[Solidworks Face object](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFace2_members.html)**.
Since üöÄ **[Solidworks Face object](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFace2_members.html)** is also an üöÄ **[Solidworks Entity object](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**, we can use methods from üöÄ **[Solidworks Entity object](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**.
:::

```vb showlinenumbers showLineNumbers
' Ask user to confirm this selection
resp = MsgBox("Is this correct Face?", vbYesNo, "Select Face")
```

* In above line, we ask user to confirm this selection.
* We do this in following steps. 
  * Show a Message Box to user as shown below.
  
  [![user-confirmation](/assets/Solidworks_Images/assembly-symmetric-mate/user-confirmation.png)](/assets/Solidworks_Images/assembly-symmetric-mate/user-confirmation.png)

  * Store user response in **`resp`** variable.

```vb showlinenumbers showLineNumbers
' Check if user response if Yes
If resp = vbYes Then

End If
```

* In above code block, we check if user response if Yes.
* We use üöÄ **[IF statement](/vba/vba-if-then-structure-select-case/)** for checking.
* **Condition**: **`resp = vbYes`**

```vb showlinenumbers showLineNumbers
' Add current face to List of Faces
Set vFaces(componentIndex + 1) = swFace
```

* When user response if **Yes**, then above code execute.
* In above line, we add current face to List of Faces.

```vb showlinenumbers showLineNumbers
' Increment face count
faceNumber = faceNumber + 1
```

* In above line, we Increment face count variable i.e. **`faceNumber`** by 1.

```vb showlinenumbers showLineNumbers
' When both faces are added
If faceNumber = 2 Then
  Exit Function
End If
```

* In above code block, we check if `faceNumber` is 2.
* We use üöÄ **[IF statement](/vba/vba-if-then-structure-select-case/)** for checking.
* **Condition**: **`faceNumber = 2`**
  * When this condition is True, then we exit **`GetFaces`** function.

```vb showlinenumbers showLineNumbers
' Get next face
Set swFace = swFace.GetNextFace
```

* In above line, we set the value of **`swFace`** variable.
* This is done by **`GetNextFace`** method of **`swFace`** variable.

### Add Symmetric Mate

In this section, we add *Symmetric Mate*.

```vb showlinenumbers showLineNumbers
' Create Assembly Mate Data
Set swMateData = swAssembly.CreateMateData(swMateType_e.swMateSYMMETRIC)
```

* In above line, we set the **value** of **`swMateData`** variable.
* We set value by **`CreateMateData()`** method.
* This **`CreateMateData()`** method is part of **`swAssembly`** variable.
* This **`CreateMateData()`** method takes following parameter.
  * **Type**: *Type of mate to create as defined in **`swMateType_e`** in below table.*
  
    | Member                        | Description |
    | ------------------------------| ----------- |
    | **`swMateANGLE`**             | *6*         |
    | **`swMateCAMFOLLOWER`**       | *9*         |
    | **`swMateCOINCIDENT`**        | *0*         |
    | **`swMateCONCENTRIC`**        | *1*         |
    | **`swMateCOORDINATE`**        | *20*        |
    | **`swMateDISTANCE`**          | *5*         |
    | **`swMateGEAR`**              | *10*        |
    | **`swMateHINGE`**             | *22*        |
    | **`swMateLINEARCOUPLER`**     |*18*         |
    | **`swMateLOCK`**              | *16*        |
    | **`swMateLOCKTOSKETCH`**      | *12*        |
    | **`swMateMAGNETIC`**          | *25*        |
    | **`swMateMAXMATES`**          | *14*        |
    | **`swMatePARALLEL`**          | *3*         |
    | **`swMatePATH`**              | *15*        |
    | **`swMatePERPENDICULAR`**     | *2*         |
    | **`swMatePROFILECENTER`**     | *24*        |
    | **`swMateRACKPINION`**        | *13*        |
    | **`swMateSCREW`**             | *17*        |
    | **`swMateSLIDER`**            | *23*        |
    | **`swMateSLOT`**              | *21*        |
    | **`swMateSYMMETRIC`**         | *8*         |
    | **`swMateTANGENT`**           | *4*         |
    | **`swMateUNIVERSALJOINT`**    | *19*        |
    | **`swMateUNKNOWN`**           | *7*         |
    | **`swMateWIDTH`**             | *11*        |
    
* Since we want to add *Symmetric mate*, hence we use value **`swMateSYMMETRIC`** as type.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IMateFeatureData_members.html)**.

```vb showlinenumbers showLineNumbers
' Set Symmetric Mate data to Assembly Mate Data
Set swSymmetricMateData = swMateData
```

* In above line, we set Symmetric Mate data to previously created Assembly Mate data.
* We set **value** of variable **`swSymmetricMateData`** to variable **`swMateData`**.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISymmetricMateFeatureData_members.html)**.

```vb showlinenumbers showLineNumbers
' Set Entities to Mate
swSymmetricMateData.EntitiesToMate = vFaces
```

* In above line, we set **Enitites** in this *Symmetric mate*.
* In our case, we want faces in **`vFaces`** variable to mate.

```vb showlinenumbers showLineNumbers
' Set Symmetric Mate faces
swSymmetricMateData.SymmetryPlane = swAssembly.FeatureByName("SYMMETRY PLANE")
```

* In above line, we set the **Symmetry plane** of this *Symmetry mate*.
* For this we select a plane we created in assembly.
  * *Plane name*: **`SYMMETRY PLANE`**
* We use **`FeatureByName()`** method to select the plane.
  * This method is part of **`swAssembly`** variable.
  * This method takes **one parameter** i.e. *Name of the feature*.
  * This method return selected object.

```vb showlinenumbers showLineNumbers
' Add Symmetric Mate
Set swMateFeature = swAssembly.CreateMate(swSymmetricMateData)
```

* In above line, we set the value of variable **`swMateFeature`** by **`CreateMate()`** method.

* This **`CreateMate()`** method takes following parameters as explained:

  - **MateData** - *Mate-specific object*:

    | Mate-specific object          |
    | ------------------------------|
    | **`IAngleMateFeatureData`**             |
    | **`ICamFollowerMateFeatureData`**       |
    | **`ICoincidentMateFeatureData`**        |
    | **`IConcentricMateFeatureData`**        |
    | **`IDistanceMateFeatureData`**          |
    | **`IGearMateFeatureData`**              |
    | **`IHingeMateFeatureData`**             |
    | **`ILinearCouplerMateFeatureData`**     |
    | **`ILockMateFeatureData`**              |
    | **`IParallelMateFeatureData `**         |
    | **`IPerpendicularMateFeatureData`**     |
    | **`IProfileCenterMateFeatureData`**     |
    | **`IRackPinionMateFeatureData`**        |
    | **`IScrewMateFeatureData`**             |
    | **`ISlotMateFeatureData`**              |
    | **`ISymmetricMateFeatureData`**         |
    | **`ITangentMateFeatureData`**           |
    | **`IUniversalJointMateFeatureData`**    |
    | **`IWidthMateFeatureData`**             |

* **Return Value** : This **`CreateMate()`** method return üöÄ **[Mate2](https://help.solidworks.com/2019/english/api/sldworksapi/SOLIDWORKS.Interop.sldworks~SOLIDWORKS.Interop.sldworks.IMate2.html)** data object.

* In our code, I have used following values:

  | Parameter Name  | Value Used   |
  | --------------- | ------------ |
  | **MateData**    | `swSymmetricMateData` |

**Reference**: For more details about 

* **Solidworks AssemblyDoc details**: üöÄ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)** for Solidworks Feature Manager.
* **CreateMate Method**: üöÄ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc~CreateMate.html)** for `CreateMate` Method.

```vb showlinenumbers showLineNumbers
' Check if Mate is added or not
If swMateFeature Is Nothing Then
  MsgBox "Failed to Add Mate."
  swDoc.ClearSelection2 True
  Exit Sub
End If
```

* In above code block, we check if we successfully added **Symmetric Mate** or not.
* We use üöÄ **[IF statement](/vba/vba-if-then-structure-select-case/)** for checking.
* **Condition**: **`swMateFeature Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/vba-msgBox-function/)** to user.
  * **Message**: *Failed to Add Mate.
  * After that we clear the selection.
  * Then we **stop** our macro here.

Now we run the macro and after running macro we get **Symmetric Mate** as shown in below image.

[![assembly-symmetric-mate](/assets/Solidworks_Images/assembly-symmetric-mate/final-result-gif.gif)](/assets/Solidworks_Images/assembly-symmetric-mate/final-result-gif.gif)

### Final work

In this section, after adding **Symmetric Mate**, we have to do some cleaning work so that we can use this macro frequently.

```vb showlinenumbers showLineNumbers
' Empty Face Array
Set vFaces(1) = Nothing
Set vFaces(2) = Nothing
```

* In above line, we empty our face array i.e. **`vFaces`** variable.
* We set value of every instance to **`Nothing`**.

```vb showlinenumbers showLineNumbers
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb showlinenumbers showLineNumbers
' View zoom to fit
swDoc.ViewZoomtofit2
```

* In above line, we make our view zoom to fit the model.
* For this we use `ViewZoomtofit2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb showlinenumbers showLineNumbers
' Rebuild assembly
swDoc.ForceRebuild3 True
```

* In above line, we Rebuild assembly.
* For this we use `ForceRebuild3` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **Symmetric Mate** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
