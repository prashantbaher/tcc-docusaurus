---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Add Perpendicular Mate
permalink: /solidworks-vba-macros/assembly-perpendicular-mate/
tags:   [Solidworks Macro]
id: 'assembly-perpendicular-mate'
---



## Objective

In this article we **create and understand** VBA macro of adding **Perpendicular Mate** in **Assembly document** of *SOLIDWORKS CAD Software*.

This method is ***most updated*** method, so ***use this method*** if you want to add **Perpendicular Mate** quickly. 

## Results We Can Get

After running our macro we successfully add **Perpendicular Mate** a Component in an Assembly as a result.

Below image shows the result we get.

[![assembly-perpendicular-mate](/assets/Solidworks_Images/assembly-perpendicular-mate/final-result-gif.gif)](/assets/Solidworks_Images/assembly-perpendicular-mate/final-result-gif.gif)

We add **Perpendicular Mate** in following steps in general.

1. *loop through each component in current assembly.*
2. *Loop through each face.*
3. *Get desired face from user confirmation.*
4. *Select required faces for mate.*
5. *Add Perpendicular Mate.*
6. *Final Work*

:::caution
To get the correct result please follow the steps correctly.
:::

## Macro Video

Below üé¨ video shows **Adding Perpendicular Mate** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/9EyWmTibQuA" frameborder="0" allowfullscreen 
width="100%"
height="500"></iframe>



***Above video is just for visualization and there are no explanation.*** 

***I have explained each and every line in this article.***

:::tip
It is advisable to watch video, since it help you to better understand the process.
:::

## VBA Macro

Below is the ***VBA macro*** for *Perpendicular Mate*.

```vb showlinenumbers showLineNumbers
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc

' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2

' Variable for Solidworks Body
Dim swBody As SldWorks.Body2

' Variable for Solidworks Mate Feature
Dim swMateFeature As SldWorks.Feature

' Variable for Solidworks Face
Dim swFace As SldWorks.Face2

' Variable for Solidworks Face List
Dim vFaces(1 To 2) As SldWorks.Face2

' Program to add Perpendicular Mate
Sub main()
  
  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox "Solidworks document is not opened."
    Exit Sub
  End If
  
  ' Set Solidworks Assembly document
  Set swAssembly = swDoc
  
  ' Variable for List of elements
  Dim vArray As Variant
  
  ' Get Components list in opened assembly
  vArray = swAssembly.GetComponents(True)
  
  ' Variable for component Index
  Dim componentIndex As Integer
  
  ' Loop Components List
  For componentIndex = 0 To UBound(vArray)
    
    ' Set Solidworks Component variable
    Set swComponent = vArray(componentIndex)
    
    ' Calling this function
    SelectFace swComponent, componentIndex
    
  Next
  
  ' Loop selected Face List
  For componentIndex = 1 To UBound(vFaces)
    
    ' Get and Set current Face
    Set swFace = vFaces(componentIndex)
    
    ' Select current Face
    swFace.Select True
    
  Next

  ' Add Perpendicular Mate
  Set swMateFeature = swAssembly.AddMate5(swMatePERPENDICULAR, swMateAlignALIGNED, False, 0, 0, 0, 0, 0, 0, 0, 0, False, False, 0, swAddMateError_ErrorUknown)

  ' Check if Mate is added or not
  If swMateFeature Is Nothing Then
    MsgBox "Failed to Add Mate."
    swDoc.ClearSelection2 True
    Exit Sub
  End If
  
  ' Clear all selection
  swDoc.ClearSelection2 True
  
  ' Zoom view to fit
  swDoc.ViewZoomtofit2
  
  ' Rebuild assembly
  swDoc.ForceRebuild3 True
  
End Sub

' Function to select required face for Perpendicular mate
Function SelectFace(component As SldWorks.Component2, componentIndex As Integer)
  
  ' Get body of current component
  Set swBody = component.GetBody
  
  ' Variable for Solidworks Select Data
  Dim swSelData As SldWorks.SelectData
  
  ' Set value of Solidworks Select Data variable
  Set swSelData = swDoc.SelectionManager.CreateSelectData
  
  ' Set Mark of Solidworks Select Data
  swSelData.Mark = 1
  
  ' Get First face
  Set swFace = swBody.GetFirstFace
  
  ' Variable to store Message Box result
  Dim resp As VbMsgBoxResult
  
  ' Loop through all Faces
  Do While Not swFace Is Nothing
    
    ' Select current face
    swFace.Select4 False, swSelData
    
    ' Ask user to confirm this selection
    resp = MsgBox("Is this correct Face?", vbYesNo, "Select Face")
    
    ' Check if user response if Yes
    If resp = vbYes Then
      ' Add current face to List of Faces
      Set vFaces(componentIndex + 1) = swFace
      Exit Function
    End If
    
    ' Get next face
    Set swFace = swFace.GetNextFace
    
  Loop
  
End Function
```

## Prerequisite

There are some *prerequisite* for this article.

* Knowledge of **VBA programming language** is ‚ùó***required***.

* We use an existing part in Assembly document.

* This component is Float as shown in below image.

[![prerequisite](/assets/Solidworks_Images/assembly-perpendicular-mate/prerequisite.gif)](/assets/Solidworks_Images/assembly-perpendicular-mate/prerequisite.gif)

:::note
We will apply checks in this article, so the code we write should be error free most of the time.
:::

## Steps To Follow

This **VBA macro** can be divided into following sections:

1. *Create and Initialize required variables*
2. *Get Components and Loop through them*
3. *Get desired face*
4. *Select faces*
5. *Add Perpendicular Mate*
6. *Final Work*

***Every section with each line is explained below.***

:::tip
I also give some ***links (see icon üöÄ)*** so that you can go through them if there are anything I explained in previous articles.
:::

### Create and Initialize required variables

```vb showlinenumbers showLineNumbers
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: üöÄ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for *Solidworks document*. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc
```

* **Purpose**: In above line, we create a variable for *Solidworks Assembly*.
* **Variable Name**: `swAssembly`
* **Type**: `SldWorks.AssemblyDoc`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2
```

* **Purpose**:  In above line, we create a variable for *Solidworks Component*.
* **Variable Name**: `swComponent` 
* **Type**: `SldWorks.Component2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IComponent2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Body
Dim swBody As SldWorks.Body2
```

* **Purpose**:  In above line, we create a variable for *Solidworks Component*.
* **Variable Name**: `swBody` 
* **Type**: `SldWorks.Body2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IBody2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Mate Feature
Dim swMateFeature As SldWorks.Feature
```

* **Purpose**:  In above line, we create a variable for *Solidworks Mate Feature*.
* **Variable Name**: `swMateFeature` 
* **Type**: `SldWorks.Feature`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeature_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Face
Dim swFace As SldWorks.Face2
```

* **Purpose**: In above line, we create a variable for *Solidworks Face*.
* **Variable Name**: `swFace` 
* **Type**: `SldWorks.Face2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFace2_members.html)**.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Face List
Dim vFaces(1 To 2) As SldWorks.Face2
```

* **Purpose**: Define üöÄ **[Array](/vba/arrays/)** of *Face2*.
* **Variable Name**: `vFaces`
* **Type**: `Face2`
* **Length of Array**: *(1 To 2)*
* **Reference**: Please visit
  * **For Arrays in VBA** : üöÄ **[Array on this website](/vba/arrays/)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb showlinenumbers showLineNumbers
' Program to add Perpendicular Mate
Sub main()

End Sub
```

* In above line, we create *main Program to add Perpendicular Mate in assembly*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

```vb showlinenumbers showLineNumbers
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb showlinenumbers showLineNumbers
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb showlinenumbers showLineNumbers
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened.*
  * Then we **stop** our macro here.

```vb showlinenumbers showLineNumbers
' Set Solidworks Assembly document
Set swAssembly = swDoc
```

* In above line, we set *value* of **`swAssembly`** variable.
* This *value* is **`swDoc`** variable.

### Get Components and Loop through them

In this section, we *Get Components and Loop through them*.

```vb showlinenumbers showLineNumbers
' Variable for List of elements
Dim vArray As Variant
```

* **Purpose**:  In above line, we create a variable for *List of elements*.
* **Variable Name**: `vArray`
* **Type**: `Variant`

```vb showlinenumbers showLineNumbers
' Get Components list in opened assembly
vArray = swAssembly.GetComponents(True)
```

* In above line, we set the value of **`vArray`** variable. 
* We set value by **`GetComponents`** method of **`swAssembly`** variable.

```vb showlinenumbers showLineNumbers
' Variable for component Index
Dim componentIndex As Integer
```

* In above line, we create a variable for component Index as a counter.
* **Variable Name**: `componentIndex`
* **Type**: `Integer`

```vb showlinenumbers showLineNumbers
' Loop Components List
For componentIndex = 0 To UBound(vArray)
  
Next
```

* In above line, we create a `For` loop.
* This loops start from `i = 0` to maximum of `vArray` we select.

```vb showlinenumbers showLineNumbers
' Set Solidworks Component variable
Set swComponent = vArray(componentIndex)
```

* In above line, we set *value* of **`swComponent`** variable.
* This *value* is current value of array **`vArray`**.

### Get desired face

In this section we get desired face for **Perpendicular mate**.

```vb showlinenumbers showLineNumbers
' Calling this function
SelectFace swComponent, componentIndex
```

* In above line, we call a function.
* **Function Name**: **`SelectFace`**
* **Function Parameters**: They are
  * **`swComponent`**
  * **`componentIndex`**

```vb showlinenumbers showLineNumbers
' Function to select required face for Perpendicular mate
Function SelectFace(component As SldWorks.Component2, componentIndex As Integer)
  
End Function
```

* In above line, we create a **Function** to select required face for Perpendicular mate.
* This is a **`Function`** procedure which has name of **`SelectFace`**. 
* This procedure hold all the *statements (instructions)* for *select required face for Perpendicular mate*.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

This function has 2 parameters as follows:
* **`component As SldWorks.Component2`**
* **`componentIndex As Integer`**

```vb showlinenumbers showLineNumbers
' Get body of current component
Set swBody = component.GetBody
```

* In above line, we set *value* of **`swBody`** variable.
* Value of **`swBody`** variable is set by **`GetBody`** method of **`component`** variable.

```vb showlinenumbers showLineNumbers
' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData
```

* **Purpose**: In above line, we create a variable for *Solidworks Select Data*.
* **Variable Name**: `swSelData`
* **Type**: `SldWorks.SelectData`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectData_members.html)**.

```vb showlinenumbers showLineNumbers
' Set value of Solidworks Select Data variable
Set swSelData = swDoc.SelectionManager.CreateSelectData
```

* In above line, we set *value* of **`swSelData`** variable.
* Value of **`swSelData`** variable is set by **`CreateSelectData`**, which is part of **`SelectionManager`**. This **`SelectionManager`** is part of **`swDoc`** variable.

```vb showlinenumbers showLineNumbers
' Set Mark of Solidworks Select Data
swSelData.Mark = 1
```

* In above line, we set *Mark* of **`swSelData`** variable to **1**.

```vb showlinenumbers showLineNumbers
' Get First face
Set swFace = swBody.GetFirstFace
```

* In above line, we set *value* of **`swFace`** variable.
* Value of **`swFace`** variable is set by **`GetFirstFace`**, which is part of **`swBody`** variable.

```vb showlinenumbers showLineNumbers
' Variable to store Message Box result
Dim resp As VbMsgBoxResult
```

* **Purpose**: In above line, we create a variable to store Message Box result.
* **Variable Name**: `resp`
* **Type**: `VbMsgBoxResult`

```vb showlinenumbers showLineNumbers
' Loop through all Faces
Do While Not swFace Is Nothing
  
Loop
```

* In above line, we loop through all Faces.

```vb showlinenumbers showLineNumbers
' Select current face
swFace.Select4 False, swSelData
```

* In above line, we select current face.

```vb showlinenumbers showLineNumbers
' Ask user to confirm this selection
resp = MsgBox("Is this correct Face?", vbYesNo, "Select Face")
```

* In above line, we ask user to confirm this selection.
* We do this in following steps.
  1. Show a Message Box to user as shown below.
  
  [![user-confirmation](/assets/Solidworks_Images/assembly-perpendicular-mate/user-confirmation.png)](/assets/Solidworks_Images/assembly-perpendicular-mate/user-confirmation.png)

  2. Store user response in **`resp`** variable.

```vb showlinenumbers showLineNumbers
' Check if user response if Yes
If resp = vbYes Then

End If
```

* In above code block, we check if user response if Yes.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`resp = vbYes`**

```vb showlinenumbers showLineNumbers
' Add current face to List of Faces
Set vFaces(componentIndex + 1) = swFace
Exit Function
```

* When user response if **Yes**, then above code execute.
* In above line, 1st we add current face to List of Faces.
* 2nd, we exit this **`SelectFace`** function.

```vb showlinenumbers showLineNumbers
' Get next face
Set swFace = swFace.GetNextFace
```

* In above line, we set the value of **`swFace`** variable.
* This is done by **`GetNextFace`** method of **`swFace`** variable. 

### Select Faces

In this section, we *Select Faces*.

```vb showlinenumbers showLineNumbers
' Loop selected Face List
For componentIndex = 1 To UBound(vFaces)
  
Next
```

* In above line, we *Loop selected Face List*.

```vb showlinenumbers showLineNumbers
' Get and Set current Face
Set swFace = vFaces(componentIndex)
```

* In above line, we set the value of **`swFace`** variable to current face of list.

```vb showlinenumbers showLineNumbers
' Select current Face
swFace.Select True
```

* In above line, we select current Face.

### Add Perpendicular Mate

In this section, we add *Perpendicular Mate*.

```vb showlinenumbers showLineNumbers
' Add Perpendicular Mate
Set swMateFeature = swAssembly.AddMate5(swMatePERPENDICULAR, swMateAlignALIGNED, False, 0, 0, 0, 0, 0, 0, 0, 0, False, False, 0, swAddMateError_ErrorUknown)
```

* In above line, we set the value of variable `swMateFeature` by `AddMate5` method.

* This `AddMate5` method takes following parameters as explained:

  - **MateTypeFromEnum** - *Type of mate as defined in **``swMateType_e``***:

    | Member                                                              | Description |
    | --------------------------------------------------------------------| ----------- |
    | **`swMateANGLE`**             | *6*         |
    | **`swMateCAMFOLLOWER`**       | *9*         |
    | **`swMateCOINCIDENT`**        | *0*         |
    | **`swMateCONCENTRIC`**        | *1*         |
    | **`swMateCOORDINATE`**        | *20*        |
    | **`swMateDISTANCE`**          | *5*         |
    | **`swMateGEAR`**              | *10*        |
    | **`swMateHINGE`**             | *22*        |
    | **`swMateLINEARCOUPLER`**     |*18*         |
    | **`swMateLOCK`**              | *16*        |
    | **`swMateLOCKTOSKETCH`**      | *12*        |
    | **`swMateMAGNETIC`**          | *25*        |
    | **`swMateMAXMATES`**          | *14*        |
    | **`swMatePARALLEL`**          | *3*         |
    | **`swMatePATH`**              | *15*        |
    | **`swMatePERPENDICULAR`**     | *2*         |
    | **`swMatePROFILECENTER`**     | *24*        |
    | **`swMateRACKPINION`**        | *13*        |
    | **`swMateSCREW`**             | *17*        |
    | **`swMateSLIDER`**            | *23*        |
    | **`swMateSLOT`**              | *21*        |
    | **`swMateSYMMETRIC`**         | *8*         |
    | **`swMateTANGENT`**           | *4*         |
    | **`swMateUNIVERSALJOINT`**    | *19*        |
    | **`swMateUNKNOWN`**           | *7*         |
    | **`swMateWIDTH`**             | *11*        |

  - **AlignFromEnum** - *Type of mate as defined in **``swMateAlign_e``***:

    | Member                                                              | Description |
    | --------------------------------------------------------------------| ----------- |
    | **`swAlignAGAINST`**                        | *Obsolete. Do not use.*         |
    | **`swAlignNONE`**                           | *Obsolete. Do not use.*         |
    | **`swAlignSAME`**                           | *Obsolete. Do not use.*         |
    | **`swMateAlignALIGNED`**                    | *0*         |
    | **`swMateAlignANTI_ALIGNED`**               | *1*         |
    | **`swMateAlignCLOSEST`**                    | *2*         |

  - **Flip** - *`True` to flip the mate entities, `False` to not; valid only if **``MateTypeFromEnum``** is **`swMatetype_e.swMateDISTANCE`***.

  - **Distance** - *Distance value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateDISTANCE`***.

  - **DistanceAbsUpperLimit** - *Absolute maximum distance value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateDISTANCE`***.

  - **DistanceAbsLowerLimit** - *Absolute minimum distance value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateDISTANCE`***.

  - **GearRatioNumerator** - *Gear ratio numerator value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateGEAR`***.

  - **GearRatioDenominator** - *Gear ratio denominator value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateGEAR`***.

  - **Angle** - *Angle value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateANGLE`***.

  - **AngleAbsUpperLimit** - *Absolute maximum angle value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateANGLE`***.

  - **AngleAbsLowerLimit** - *Absolute minimum angle value; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateANGLE`***.

  - **ForPositioningOnly** - *`True` to only position the components according to the mating relationship and not return a mate, `False` to return a mate*.

  - **LockRotation** - *`True` to lock component rotation, `False` to not*.

  - **WidthMateOption** - *Width mate options as defined in **`swMateWidthOptions_e`**; valid only if **`MateTypeFromEnum`** is **`swMateType_e.swMateWIDTH`***.

    | Member                                                              | Description |
    | --------------------------------------------------------------------| ----------- |
    | **`swMateWidth_Centered`**    | *0*         |
    | **`swMateWidth_Dimension`**   | *2*         |
    | **`swMateWidth_Free`**        | *1*         |
    | **`swMateWidth_Percent`**     | *3*         |

  - **ErrorStatus** - *Success or error as defined by **`swAddMateError_e`** as follows*.

    | Member                                                              | Description |
    | --------------------------------------------------------------------| ----------- |
    | **`swAddMateError_ErrorUknown`**          | *0*         |
    | **`swAddMateError_IncorrectAlignment`**   | *3*         |
    | **`swAddMateError_IncorrectGearRatios`**  | *6*         |
    | **`swAddMateError_IncorrectMateType`**    | *2*         |
    | **`swAddMateError_IncorrectSelections`**  | *4*         |
    | **`swAddMateError_NoError`**              | *1*         |
    | **`swAddMateError_OverDefinedAssembly`**  | *5*         |

* **Return Value** : This `AddMate5` method return üöÄ **[Mate2](https://help.solidworks.com/2019/english/api/sldworksapi/SOLIDWORKS.Interop.sldworks~SOLIDWORKS.Interop.sldworks.IMate2.html)** data object.

* In our code, I have used following values:

  | Parameter Name  | Value Used   |
  | --------------- | ------------ |
  | **MateTypeFromEnum**        | `swMatePERPENDICULAR`       |
  | **AlignFromEnum**           | `swMateAlignALIGNED`        |
  | **Flip**                    | `false`                     |
  | **Distance**                | `0`                         |
  | **DistanceAbsUpperLimit**   | `0`                         |
  | **DistanceAbsLowerLimit**   | `0`                         |
  | **GearRatioNumerator**      | `0`                         |
  | **GearRatioDenominator**    | `0`                         |
  | **Angle**                   | `0`                         |
  | **AngleAbsUpperLimit**      | `0`                         |
  | **AngleAbsLowerLimit**      | `0`                         |
  | **ForPositioningOnly**      | `False`                     |
  | **LockRotation**            | `False`                     |
  | **WidthMateOption**         | `0`                         |
  | **ErrorStatus**             | `swAddMateError_ErrorUknown`|

**Reference**: For more details about 

* **Solidworks AssemblyDoc details**: üöÄ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)** for Solidworks Feature Manager.
* **AddMate5 Method**: üöÄ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc~AddMate5.html)** for `AddMate5` Method.

```vb showlinenumbers showLineNumbers
' Check if Mate is added or not
If swMateFeature Is Nothing Then
  MsgBox "Failed to Add Mate."
  swDoc.ClearSelection2 True
  Exit Sub
End If
```

* In above code block, we check if we successfully added **Perpendicular Mate** or not.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swMateFeature Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Failed to Add Mate.
  * After that we clear the selection.
  * Then we **stop** our macro here.

Now we run the macro and after running macro we get **Perpendicular Mate** as shown in below image.

[![assembly-perpendicular-mate](/assets/Solidworks_Images/assembly-perpendicular-mate/final-result-gif.gif)](/assets/Solidworks_Images/assembly-perpendicular-mate/final-result-gif.gif)

### Final work

In this section, after adding **Perpendicular Mate**, we have to do some cleaning work so that we can use this macro frequently.

```vb showlinenumbers showLineNumbers
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb showlinenumbers showLineNumbers
' View zoom to fit
swDoc.ViewZoomtofit2
```

* In above line, we make our view zoom to fit the model.
* For this we use `ViewZoomtofit2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb showlinenumbers showLineNumbers
' Rebuild assembly
swDoc.ForceRebuild3 True
```

* In above line, we Rebuild assembly.
* For this we use `ForceRebuild3` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **Perpendicular Mate** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
