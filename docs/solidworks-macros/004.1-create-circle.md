---
categories: Solidworks-macro
title:  Solidworks Macro - Create Circle
permalink: /solidworks-macros/create-circle/
tags:   [Solidworks Macro]
id: 'create-circle'
---

import AdComponent from '@site/src/components/Adsense';

<AdComponent />

In this post, I tell you about *how to create Circle through Solidworks VBA Macros* in a sketch.

The process is almost identical with previous ðŸš€ **[Solidworks Sketch Macros - Create Line](/solidworks-macros/sketch-create-line)** post.

In this post, I tell you about `CreateCircle` method from **Solidworks** `SketchManager` object.

This method is ***most updated*** method, I found in *Solidworks API Help*. 

So ***use this method*** if you want to create a new **Circle**.

---

## Video of Code on YouTube

Please see below video ðŸŽ¬ on **how to create Circle** from Solidworks VBA Macros.

<iframe src="https://www.youtube.com/embed/Y5fh4DS_caM" frameborder="0" allowfullscreen 
width="100%"
height="500"></iframe>

Please note that there are **no explaination** in the video. 

**Explaination** of each line and why we write code this way is given in this post.

---

## Code Sample

Below is the `code` sample for creating *a Circle*.

```vb showlinenumbers showLineNumbers
Option Explicit

' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
' Boolean Variable
Dim BoolStatus As Boolean
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager

' Main function of our VBA program
Sub main()

  ' Setting Solidworks variable to Solidworks application
  Set swApp = Application.SldWorks
  
  ' Creating string type variable for storing default part location
  Dim defaultTemplate As String
  ' Setting value of this string type variable to "Default part template"
  defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)

  ' Setting Solidworks document to new part document
  Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)

  ' Selecting Front Plane
  BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
  
  ' Setting Sketch manager for our sketch
  Set swSketchManager = swDoc.SketchManager
  
  ' Creating Variable for Solidworks Sketch segment
  Dim mySketchSegment As SketchSegment
  
  ' Inserting a sketch into selected plane
  swSketchManager.InsertSketch True
  
  ' Creating a circle
  Set mySketchSegment = swSketchManager.CreateCircle(0, 0, 0, 1, 0, 0)
  
  ' De-select the line after creation
  swDoc.ClearSelection2 True

  ' Zoom to fit screen in Solidworks Window
  swDoc.ViewZoomtofit

End Sub
```

---

<AdComponent />

## Understanding the Code

Now let us walk through *each line* in the above code, and **understand** the meaning of every line.

```vb showlinenumbers showLineNumbers
Option Explicit
```

This line forces us to define every variable we are going to use. 

:::tip
For more information please visit ðŸš€ **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.
:::

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

In this line, we are creating a variable which we named as `swApp` and the type of this `swApp` variable is `SldWorks.SldWorks`.

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

In this line, we are creating a variable which we named as `swDoc` and the type of this `swDoc` variable is `SldWorks.ModelDoc2`.

Next is our `Sub` procedure named `main`. This procedure hold all the *statements (instructions)* we give to computer.

```vb showlinenumbers showLineNumbers
' Setting Solidworks variable to Solidworks application
Set swApp = Application.SldWorks
```

In this line, we are setting the value of our Solidworks variable which we define earlier to Solidworks application.

```vb showlinenumbers showLineNumbers
' Creating string type variable for storing default part location
Dim defaultTemplate As String
' Setting value of this string type variable to "Default part template"
defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)
```

In 1st statement of above example, we are defining a variable of `string` type and named it as `defaultTemplate`.

This variable `defaultTemplate`, hold the location the location of **Default Part Template**.

In 2nd line of above example. we assign value to our newly define `defaultTemplate` variable.

We assign the value by using a *Method* named `GetUserPreferenceStringValue()`. This method is a part of our main Solidworks variable `swApp`.

```vb showlinenumbers showLineNumbers
' Setting Solidworks document to new part document
Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)
```

In this line, we set the value of our `swDoc` variable to new document.

:::tip
For **detailed information** about these lines please visit ðŸš€ **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.
:::

```vb showlinenumbers showLineNumbers
' Boolean Variable
Dim BoolStatus As Boolean

' Selecting Front Plane
BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
```

In 1st line, we create a variable named `BoolStatus` as `Boolean` object.

In next line, we select the *front plane* by using `SelectByID2` method from `Extension` object.

:::tip
For more information about selection method please visit ðŸš€ **[Solidworks Macros - Selection Methods](/solidworks-macros/select-plane-from-tree)** post.
:::

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager
```

In above line, we create variable `swSketchManager` for **Solidworks Sketch Manager**.

As the name suggested, a Sketch Manager holds variours methods and properties to manage Sketches.

To see methods and properties related to SketchManager object, please visit ðŸš€ **[this page of Solidworks API](https://help.solidworks.com/2017/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchManager_members.html)**

```vb showlinenumbers showLineNumbers
' Setting Sketch manager for our sketch
Set swSketchManager = swDoc.SketchManager
```

In above line, we set the sketch manager variable to current document's sketch manager.

```vb showlinenumbers showLineNumbers
' Inserting a sketch into selected plane
swSketchManager.InsertSketch True
```

In above line, we use `InsertSketch` method of *SketchManager* and give `True` value.

This method allows us to insert a sketch in selected plane.

```vb showlinenumbers showLineNumbers
' Creating Variable for Solidworks Sketch segment
Dim mySketchSegment As SketchSegment

' Creating a Circle
Set mySketchSegment = swSketchManager.CreateCircle(0, 0, 0, 1, 0, 0)
```

In above sample code, we 1st create a variable named `mySketchSegment` of type `SketchSegment`.

A `SketchSegment` represent *a line, ellipse, parabola or spline.*

A `SketchSegment` provides functions that are **generic** to every type of sketch segment.

For example, every sketch segment has **an ID** and can be selected programmatically.

Therefore, the `SketchSegment` interface provides functions to obtain the ID and to select the item.

For detailed information about the `SketchSegment` please visit ðŸš€ **[this page of Solidworks API Help](http://help.solidworks.com/2017/english/api/sldworksapi/SOLIDWORKS.Interop.sldworks~SOLIDWORKS.Interop.sldworks.ISketchSegment.html)**

In 2nd line, we set the value of sketch segment variable `mySketchSegment`.

We get this value from `CreateCircle` method which is inside the `swSketchManager` variable.

`swSketchManager` variable is a type of SketchManager, hence we used `CreateCircle` method from SketchManager.

This `CreateCircle` method takes following parameters as explained:

*XC* : X coordinate of the circle center point

*YC* : Y coordinate of the circle center point

*ZC* : Z coordinate of the circle center point

*XP* : X coordinate of the point on the circle perimeter

*YP* : Y coordinate of the point on the circle perimeter

*ZP* : Z coordinate of the point on the circle perimeter

In the above code sample I have used origin point (0, 0, 0) for circle center point.

This is origin of sketch hence I use center point at origin.

For End point I used (0, 2, 0) which is 2 point distance in Y-direction or vertical direction.

:::tip NOTE

It is ***very important*** to remember that, when you give distance or any other numeric value in **Solidworks API**, Solidworks takes that numeric value in ***Meter only***.

*Solidworks API* does not care about your application's Unit systems.

For example, I works in ANSI system means "inches" for distance. 

But when I used Solidworks API through *VBA macros* or *C#*, I have to use **converted** numeric values.

Because Solidworks API output the distance in **Meter** only; which is not my requirement.
:::

```vb showlinenumbers showLineNumbers
' De-select the line after creation
swDoc.ClearSelection2 True
```

In the this line of code, we de-select the created line.

For de-selecting, we use `ClearSelection2` method from our Solidworks document variable `swDoc`.

```vb showlinenumbers showLineNumbers
' Zoom to fit screen in Solidworks Window
swDoc.ViewZoomtofit
```

In this last line we use *zoom to fit* command.

For Zoom to fit, we use `ViewZoomtofit` method from our Solidworks document variable `swDoc`. 

Hope this post helps you to *create Circle* in Sketches with Solidworks VB Macros.

For more such tutorials on **Solidworks VBA Macros**, do come to this blog after sometime.

Till then, Happy learning!!!

