---
categories: Solidworks-macro
title:  Solidworks Macro - Create a Chamfer
permalink: /solidworks-macros/create-sketch-chamfer/
tags:   [Solidworks Macro]
id: 'create-sketch-chamfer'
---

In this post, I tell you about *how to create a Chamfer through Solidworks VBA Macros* in a sketch.

This post is an extension of **[Sketch - Create Corner Rectangle](/solidworks-macros/create-corner-rectangle)** post.

---

## Video of Code on YouTube

Please see below video how visually we can create *a Chamfer* from **Solidworks VBA macro**.

<iframe src="https://www.youtube.com/embed/HobbXAv9zMI" frameborder="0" allowfullscreen 
width="100%"
height="500"></iframe>

Please note that there are **no explaination** given in the video. 

**Explaination** of each line and why we write code this way is given in this post.

---

## For Experience Macro Developer

If you are an experience **Solidworks Macro developer**, then you are looking for a specific code sample.

Below is the code for creating **A Chamfer** from **Solidworks VBA Macro**.

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks Sketch Segment
Dim swSketchSegment As SldWorks.SketchSegment
      
' Set the value of Solidworks Sketch segment by "CreateChamfer" method from Solidworks sketch manager
Set swSketchSegment = swSketchManager.CreateChamfer(swSketchChamferType_e.swSketchChamfer_DistanceEqual, 0.1, 0.2)
```

For creating a **Chamfer** first you need to **Create** a variable of `SketchSegment` type.

After creating variable, you need to set the value of this variable.

For this you used `CreateChamfer` method from **Solidworks Sketch Manager**.

This `CreateChamfer` method set the value of `SketchSegment` type variable.

This `CreateChamfer` method takes following parameters as explained:

- **Type** : *Type of chamfer as defined in `swSketchChamferType_e`*

- **Distance** : *Distance of the chamfer*

- **AngleORdist** : *These are as follows*

  * If Type = `swSketchChamfer_DistanceDistance`, then the second chamfer distance 

  * If Type = `swSketchChamfer_DistanceAngle`, then the second chamfer angle 

  * If Type = `swSketchChamfer_DistanceEqual`, then this argument is ignored because Distance is used for both edges

If you want a more detail explaination then please read further otherwise this will help you to **Create a Chamfer From VBA Macro**.

---

## For Beginners Macro Developers

In this post, I tell you about `CreateChamfer` method from **Solidworks** `SketchManager` object.

This method is ***most updated*** method, I found in *Solidworks API Help*. 

So ***use this method*** if you want to create a Chamfer.

Below is the `code` sample for creating a Chamfer.

```vb showlinenumbers showLineNumbers
Option Explicit

' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks

' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Boolean Variable
Dim BoolStatus As Boolean

' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager

' Creating Variable for Solidworks Sketch Segment
Dim swSketchSegment As SldWorks.SketchSegment


' Main function of our VBA program
Sub main()

  ' Setting Solidworks variable to Solidworks application
  Set swApp = Application.SldWorks
  
  ' Creating string type variable for storing default part location
  Dim defaultTemplate As String
  ' Setting value of this string type variable to "Default part template"
  defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)

  ' Setting Solidworks document to new part document
  Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)

  ' Selecting Front Plane
  BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)

  ' Setting Sketch manager for our sketch
  Set swSketchManager = swDoc.SketchManager

  ' Inserting a sketch into selected plane
  swSketchManager.InsertSketch True
  
  ' Creating a "Variant" Variable which holds the values return by "CreateCornerRectangle" method
  Dim vSketchLines As Variant
  
  ' Creating a Corner Rectangle
  vSketchLines = swSketchManager.CreateCornerRectangle(0, 1, 0, 1, 0, 0)
  
  ' De-select the lines after creation
  swDoc.ClearSelection2 True
  
  ' Selecting Point 1
  BoolStatus = swDoc.Extension.SelectByID2("Point1", "SKETCHPOINT", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)

  ' Set the value of Solidworks Sketch segment by "CreateChamfer" method from Solidworks sketch manager
  Set swSketchSegment = swSketchManager.CreateChamfer(swSketchChamferType_e.swSketchChamfer_DistanceEqual, 0.1, 0.2)

  ' De-select the Chamfer after creation
  swDoc.ClearSelection2 True
  
  ' Show Front View after creating Chamfer
  swDoc.ShowNamedView2 "", swStandardViews_e.swFrontView
  
  ' Zoom to fit screen in Solidworks Window
  swDoc.ViewZoomtofit2

End Sub
```

---

### Understanding the Code

Now let us walk through *each line* in the above code, and **understand** the meaning of every line.

```vb showlinenumbers showLineNumbers
Option Explicit
```

This line forces us to define every variable we are going to use. 

For more information please visit **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

In this line, we are creating a variable which we named as `swApp` and the type of this `swApp` variable is `SldWorks.SldWorks`.

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

In this line, we are creating a variable which we named as `swDoc` and the type of this `swDoc` variable is `SldWorks.ModelDoc2`.

```vb showlinenumbers showLineNumbers
' Boolean Variable
Dim BoolStatus As Boolean
```

In this line, we create a variable named `BoolStatus` as `Boolean` object type.

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager
```

In above line, we create variable `swSketchManager` for **Solidworks Sketch Manager**.

As the name suggested, a **Sketch Manager** holds variours methods and properties to manage *Sketches*.

To see methods and properties related to `SketchManager` object, please visit **[this page of Solidworks API Help](http://help.solidworks.com/2017/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchManager_members.html)**

```vb showlinenumbers showLineNumbers
' Creating variable for Solidworks Sketch Segment
Dim swSketchSegment As SldWorks.SketchSegment
```

In this line, we are creating a variable which we named as `swSketchSegment` and the type of this `swSketchSegment` variable is `SldWorks.SketchSegment`.

We create variable `swSketchSegment` for **Solidworks Sketch Segments**.

To see methods and properties related to `swSketchSegment` object, please visit **[this page of Solidworks API Help](http://help.solidworks.com/2019/English/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchSegment_members.html)**

These all are our global variables.

As you can see in code sample, they are **Solidworks API Objects**.

So basically I group all the **Solidworks API Objects** in one place.

I have also place `boolean` type object at top also, because after certain point we will *need* this variable frequently.

Thus, I have started placing it here.

Next is our `Sub` procedure named `main`. This procedure hold all the *statements (instructions)* we give to computer.

```vb showlinenumbers showLineNumbers
' Setting Solidworks variable to Solidworks application
Set swApp = Application.SldWorks
```

In this line, we are setting the value of our Solidworks variable which we define earlier to Solidworks application.

```vb showlinenumbers showLineNumbers
' Creating string type variable for storing default part location
Dim defaultTemplate As String
' Setting value of this string type variable to "Default part template"
defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)
```

In 1st statement of above example, we are defining a variable of `string` type and named it as `defaultTemplate`.

This variable `defaultTemplate`, hold the location the location of **Default Part Template**.

In 2nd line of above example. we assign value to our newly define `defaultTemplate` variable.

We assign the value by using a *Method* named `GetUserPreferenceStringValue()`. This method is a part of our main Solidworks variable `swApp`.

```vb showlinenumbers showLineNumbers
' Setting Solidworks document to new part document
Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)
```

In this line, we set the value of our `swDoc` variable to new document.

For **detailed information** about these lines please visit **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.

I have discussed them **thoroghly** in **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post, so do checkout this post if you don't understand above code.

```vb showlinenumbers showLineNumbers
' Selecting Front Plane
BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
```

In above line, we select the *front plane* by using `SelectByID2` method from `Extension` object.

For more information about selection method please visit **[Solidworks Macros - Selection Methods](/solidworks-macros/select-plane-from-tree)** post.

```vb showlinenumbers showLineNumbers
' Setting Sketch manager for our sketch
Set swSketchManager = swDoc.SketchManager
```

In above line, we set the sketch manager variable to current document's sketch manager.

```vb showlinenumbers showLineNumbers
' Inserting a sketch into selected plane
swSketchManager.InsertSketch True
```

In above line, we use `InsertSketch` method of *SketchManager* and give `True` value.

This method allows us to insert a sketch in selected plane.

```vb showlinenumbers showLineNumbers
' Creating a "Variant" Variable which holds the values return by "CreateCornerRectangle" method
Dim vSketchLines As Variant
    
' Creating a Corner Rectangle
vSketchLines = swSketchManager.CreateCornerRectangle(0, 1, 0, 1, 0, 0)
```

In above sample code, we 1st create a variable named `vSketchLines` of type `Variant`.

A `Variant` type variable can hold **any** type of value depends upon the use of variable.

In 2nd line, we set the value of variable `vSketchLines`.

Value of `vSketchLinesis` an array of lines. This array is send as return value when we use `CreateCornerRectangle` method.

This `CreateCornerRectangle` method is part of `swSketchManager` and it is the latest method to create a corner rectangle.

For detail explaination on `CreateCornerRectangle` method, please see **[Sketch - Create Corner Rectangle](/solidworks-macros/create-corner-rectangle)** post.

In the above code sample I have used (0, 1, 0) Upper-left point in *Y-direction*.

For Lower-right point I used (1, 0, 0) which is 1 point distance in *X-direction*.

```vb showlinenumbers showLineNumbers
' De-select the Rectangle after creation
swDoc.ClearSelection2 True
```

In above line, we de-select the ractangle we just create.

```vb showlinenumbers showLineNumbers
' Selecting Front Plane
BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
```

In above line, we select the *front plane* by using `SelectByID2` method from `Extension` object.

For more information about selection method please visit **[Solidworks Macros - Selection Methods](/solidworks-macros/select-plane-from-tree)** post.

```vb showlinenumbers showLineNumbers
' Set the value of Solidworks Sketch segment by "CreateChamfer" method from Solidworks sketch manager
Set swSketchSegment = swSketchManager.CreateChamfer(swSketchChamferType_e.swSketchChamfer_DistanceEqual, 0.1, 0.2)
```

In above line, we set the value of Solidworks Sketch Segment variable `swSketchSegment` by `CreateChamfer` method from *Solidworks Sketch Manager*.

This `CreateChamfer` method takes following parameters:

**Type** : *Type of chamfer as defined in `swSketchChamferType_e`*.

The `swSketchChamferType_e` has 3 values for type of chamfers:

  * `swSketchChamfer_DistanceAngle`

  * `swSketchChamfer_DistanceDistance`

  * `swSketchChamfer_DistanceEqual`

**Distance** : *Distance of the chamfer*

**AngleORdist** : *Angle or Distance for chamfer. These are as follows*

  * If Type = `swSketchChamfer_DistanceDistance`, then the second chamfer distance 

  * If Type = `swSketchChamfer_DistanceAngle`, then the second chamfer angle 

  * If Type = `swSketchChamfer_DistanceEqual`, then this argument is ignored because Distance is used for both edges.

Below Image described **the Parameters for a Chamfer**.

![fillet_parameters](/assets/Solidworks_Images/fillet_and_chamfer/fillet_parameters.png)

In our code, I have used following values:

  - **Type** : I have used `swSketchChamferType_e.swSketchChamfer_DistanceEqual` enumerator as value for type of Chamfer.

  - **Distance** : I have used 0.1 (This value is in meter) as the distance of Chamfer.

  - **AngleORdist** : I have used 0.2 (This value is in meter). But in our code **Type = `swSketchChamfer_DistanceEqual`**, then this argument is ignored because Distance
  is used for both edges.

---

### NOTE

It is ***very important*** to remember that, when you give distance or any other numeric value in **Solidworks API**, Solidworks takes that numeric value in ***Meter only***.

Solidworks API does not care about your application's Unit systems.

For example, I works in **ANSI** system means inches for distance. But when I used **Solidworks API** through *VBA macros or C#*, I need to use converted numeric values.

Because Solidworks API output the distance in **Meter** which is not my requirement.

```vb showlinenumbers showLineNumbers
' De-select the Fillet after creation
swDoc.ClearSelection2 True
```

In the above line of code, we deselect the **Chamfer** we have created.

For de-selecting, we use `ClearSelection2` method from our Solidworks document name `swDoc`.

```vb showlinenumbers showLineNumbers
' Show Front View after creating Chamfer
swDoc.ShowNamedView2 "", swStandardViews_e.swFrontView
```

In the above line of code, we update the *view orientation* to **Front View**.

In my machine, after inserting a sketch view orientation does not changed.

Because of this I have to update the view to **Front view**.

For showing **Front View** we used `ShowNamedView2` method from our Solidworks document name `swDoc`.

This method takes 2 parameter described as follows:

**VName** : Name of the view to display or an empty string to use ViewId instead

**ViewId** : ID of the view to display as defined by `swStandardViews_e` or -1 to use the **VName** argument instead.

*NOTE:* If you specify both **VName** and **ViewId**, then **ViewId** takes precedence if the two arguments do not resolve to the same view.

`swStandardViews_e` has following Standard View Types:

  - *swBackView*

  - *swBottomView*

  - *swDimetricView*

  - *swFrontView*

  - *swIsometricView*

  - *swLeftView*

  - *swRightView*

  - *swTopView*

  - *swTrimetricView*

In our code, we did not use **VName** instead I used *empty string* in form of ***""*** symbol.

I used **ViewId** value to specify view and used `swStandardViews_e.swFrontView` value to use *Standard Front View*.

```vb showlinenumbers showLineNumbers
' Zoom to fit screen in Solidworks Window
swDoc.ViewZoomtofit
```

In this last line we use *zoom to fit* command.

For Zoom to fit, we use `ViewZoomtofit` method from our Solidworks document variable `swDoc`.

This is it !!!

If you found anything to add or update, please let me know on my e-mail.

---

## VBA Language feature used in this post

In this post used some features of **VBA programming language**.

This section of post, has some brief information about the VBA programming language specific features.

1. We use **Option Explicit** for capturing un-declared variables. If you want to read more about **Option Explicit** then please visit **[Declaring and Scoping of Variables](/vba/declaring-and-scoping-of-variables)**.

2. Then we create **variable** for different data types. If you don't know about them, then please visit **[Variables](/vba/variables)** and **[Data-types](/vba/programming-concepts/#data-types-in-vba)** posts of this blog. These posts will help you to understand what **Variables** are and how to use them.

3. Then we create **main Sub procedure** for our macro. If you don't know about the **Sub procedure**, then I suggest you to visit **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure)** and **[Executing Sub and Function Procedures](/vba/executing-procedures)** posts of this blog. These posts will help you to understand what **Procedures** are and how to use them.

4. In most part we create some variables and set their values. We set those values by using some **functions** provided from objects. If you don't know about the **functions**, then you should visit **[VBA Functions](/vba/functions)** and **[VBA Functions that do more](/vba/more-functions)** posts of this blog. These posts will help you to understand what **functions** are and how to use them.

---

## Solidworks API Objects

In this post, for creating a **Fillet**, we use *Solidworks API objects and their methods*.

This section contains the list of all **Solidworks Objects** used in this post.

I have also attached links of these **Solidworks API Objects** in **API Help website**.

If you want to explore those objects, you can use these links.

These Solidworks API Objects are listed below:

- **Solidworks Application Object**

If you want explore ***Properties and Methods/Functions*** of **Solidworks Application Object** object you can visit **[this link](http://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

- **Solidworks Document Object**

If you want explore ***Properties and Methods/Functions*** of **Solidworks Document Object** object you can visit **[this link](http://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

- **Solidworks Sketch Manager Object**

If you want explore ***Properties and Methods/Functions*** of **Solidworks Sketch Manager Object** you can visit **[this link](http://help.solidworks.com/2017/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchManager_members.html)**.

- **Solidworks Sketch Segment Object**

If you want explore ***Properties and Methods/Functions*** of **Solidworks Sketch Segment Object** you can visit **[this link](http://help.solidworks.com/2019/English/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchSegment_members.html)**.

---

Hope this post helps you to *create a Chamfer* in Sketches with Solidworks VB Macros.

For more such tutorials on **Solidworks VBA Macros**, do come to this blog after sometime.

Till then, Happy learning!!!
