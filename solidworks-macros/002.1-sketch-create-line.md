---
categories: Solidworks-macro
title:  Solidworks Sketch Macro - Create Line 
permalink: /solidworks-macros/sketch-create-line/
tags:   [Solidworks Macro]
---

In this post, I tell you about *how to create 2D Line through Solidworks VBA Macros* in a sketch.

For this, I take the example from previous **[Solidworks Macros - Open Assembly and Drawing document](/solidworks-macros/select-plane-from-tree)** post.

In this post, I tell you about `CreateLine` method from **Solidworks** `SketchManager` object.

This method is ***most updated*** method, I found in *Solidworks API Help*. 

So ***use this method*** if you want to create a new line.

---

## Video of Code on YouTube

Please see below video on **how to create 2D Line** from Solidworks VBA Macros.

<iframe src="https://www.youtube.com/embed/qDvQF8xBCSk" frameborder="0" allowfullscreen></iframe>


Please note that there are **no explaination** in the video. 

**Explaination** of each line and why we write code this way is given in this post.

---

## Code Sample

Below is the `code` sample for creating lines.

```vb
Option Explicit

' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
' Boolean Variable
Dim BoolStatus As Boolean
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager

' Main function of our VBA program
Sub main()

    ' Setting Solidworks variable to Solidworks application
    Set swApp = Application.SldWorks
    
    ' Creating string type variable for storing default part location
    Dim defaultTemplate As String
    ' Setting value of this string type variable to "Default part template"
    defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)

    ' Setting Solidworks document to new part document
    Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)

    ' Selecting Front Plane
    BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
    
    ' Setting Sketch manager for our sketch
    Set swSketchManager = swDoc.SketchManager
    
    ' Creating Variable for Solidworks Sketch segment
    Dim mySketchSegment As SketchSegment
    
    ' Inserting a sketch into selected plane
    swSketchManager.InsertSketch True
    
    ' Creating an horizontal line
    Set mySketchSegment = swSketchManager.CreateLine(0, 0, 0, 2, 0, 0)
    
    ' De-select the line after creation
    swDoc.ClearSelection2 True

End Sub
```

### Understanding Code

Now let us walk through *each line* in the above code, and **understand** the meaning of every line.

```vb
Option Explicit
```

This line forces us to define every variable we are going to use. 

For more information please visit **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.

```vb
' Creating variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

In this line, we are creating a variable which we named as `swApp` and the type of this `swApp` variable is `SldWorks.SldWorks`.

```vb
' Creating variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

In this line, we are creating a variable which we named as `swDoc` and the type of this `swDoc` variable is `SldWorks.ModelDoc2`.

Next is our `Sub` procedure named `main`. This procedure hold all the *statements (instructions)* we give to computer.

```vb
' Setting Solidworks variable to Solidworks application
Set swApp = Application.SldWorks
```

In this line, we are setting the value of our Solidworks variable which we define earlier to Solidworks application.

```vb
' Creating string type variable for storing default part location
Dim defaultTemplate As String
' Setting value of this string type variable to "Default part template"
defaultTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplatePart)
```

In 1st statement of above example, we are defining a variable of `string` type and named it as `defaultTemplate`.

This variable `defaultTemplate`, hold the location the location of **Default Part Template**.

In 2nd line of above example. we assign value to our newly define `defaultTemplate` variable.

We assign the value by using a *Method* named `GetUserPreferenceStringValue()`. This method is a part of our main Solidworks variable `swApp`.

```vb
' Setting Solidworks document to new part document
Set swDoc = swApp.NewDocument(defaultTemplate, 0, 0, 0)
```

In this line, we set the value of our `swDoc` variable to new document.

For **detailed information** about these lines please visit **[Solidworks Macros - Open new Part document](/solidworks-macros/open-new-document)** post.

```vb
' Boolean Variable
Dim BoolStatus As Boolean

' Selecting Front Plane
BoolStatus = swDoc.Extension.SelectByID2("Front Plane", "PLANE", 0, 0, 0, False, 0, Nothing, swSelectOption_e.swSelectOptionDefault)
```

In 1st line, we create a variable named `BoolStatus` as `Boolean` object.

In next line, we select the *front plane* by using `SelectByID2` method from `Extension` object.

For more information about selection method please visit **[Solidworks Macros - Selection Methods](/solidworks-macros/select-plane-from-tree)** post.

```vb
' Creating variable for Solidworks Sketch Manager
Dim swSketchManager As SldWorks.SketchManager
```

In above line, we create variable `swSketchManager` for **Solidworks Sketch Manager**.

As the name suggested, a Sketch Manager holds variours methods and properties to manage Sketches.

To see methods and properties related to SketchManager object, please visit **[this page](https://help.solidworks.com/2017/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISketchManager_members.html)**

```vb
' Setting Sketch manager for our sketch
Set swSketchManager = swDoc.SketchManager
```

In above line, we set the sketch manager variable to current document's sketch manager.

```vb
' Inserting a sketch into selected plane
swSketchManager.InsertSketch True
```

In above line, we use `InsertSketch` method of *SketchManager* and give `True` value.

This method allows us to insert a sketch in selected plane.

```vb
' Creating Variable for Solidworks Sketch segment
Dim mySketchSegment As SketchSegment

' Creating an horizontal line
Set mySketchSegment = swSketchManager.CreateLine(0, 0, 0, 2, 0, 0)
```

In above sample code, we 1st create a variable named `mySketchSegment` of type `SketchSegment`.

A `SketchSegment` represent *a line, ellipse, parabola or spline.*

A `SketchSegment` provides functions that are **generic** to every type of sketch segment.

For example, every sketch segment has **an ID** and can be selected programmatically.

Therefore, the `SketchSegment` interface provides functions to obtain the ID and to select the item.

For detailed information about the `SketchSegment` please visit **[this page of Solidworks API Help](http://help.solidworks.com/2017/english/api/sldworksapi/SOLIDWORKS.Interop.sldworks~SOLIDWORKS.Interop.sldworks.ISketchSegment.html)**

In 2nd line, we set the value of sketch segment variable `mySketchSegment`.

We get this value from `CreateLine` method which is inside the `swSketchManager` variable.

`swSketchManager` variable is a type of SketchManager, hence we used `CreateLine` method from SketchManager.

This `CreateLine` method takes following parameters as explained:

- *X1* : X coordinate of the line start point

- *Y1* : Y coordinate of the line start point

- *Z1* : Z coordinate of the line start point

- *X2* : X coordinate of the line end point

- *Y2* : Y coordinate of the line end point

- *Z2* : Z coordinate of the line end point

In the above code sample I have used (0, 0, 0) for start point.

This is origin of sketch hence I start line from origin.

For End point I used (2, 0, 0) which is 2 point distance in X-direction or horizontal direction.

### NOTE

It is ***very important*** to remember that, when you give distance or any other numeric value in **Solidworks API**, Solidworks takes that numeric value in ***Meter only***.

Solidworks API does not care about your application's Unit systems.

For example, I works in ANSI system means inches for distance. But when I used Solidworks API through VBA macros or C#, I need to use converted numeric values.

Because Solidworks API output the distance in **Meter** which is not my requirement.

```vb
' De-select the line after creation
swDoc.ClearSelection2 True
```

In the last line of code, we de-select the created line.

For de-selecting, we use `ClearSelection2` method from our Solidworks document name `swDoc`.

Hope this post helps you to *create lines* in Sketches with Solidworks VB Macros.

For more such tutorials on **Solidworks VBA Macros**, do come to this blog after sometime.

Till then, Happy learning!!!
