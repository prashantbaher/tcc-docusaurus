---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Unfix Component
permalink: /solidworks-vba-macros/assembly-unfix-component/
tags:   [Solidworks Macro]

---

{% include article-notes/intermediate-note.html %}

## Objective

In this article we **create and understand** VBA macro of **Unfix Component(s)** in **Assembly document** of *SOLIDWORKS CAD Software*.

This method is ***most updated*** method, so ***use this method*** if you want to **Unfix Component(s)** quickly. 

## Results We Can Get

After running our macro we successfully **Unfix Component(s)** a Component in an Assembly as a result.

Below image shows the result we get.

[![unfix-component-final-result](/assets/Solidworks_Images/assembly-Unfix-component/final-result-gif.gif)](/assets/Solidworks_Images/assembly-Unfix-component/final-result-gif.gif)

We **Unfix Component** in following steps in general.

1. *Unfix an open Part in new Assembly document.*

To get the correct result please follow the steps correctly.
{: .notice--warning}

## Macro Video

Below üé¨ video shows **Unfix Component** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/2-_xYMr7i7Y" frameborder="0" allowfullscreen></iframe>



***Above video is just for visualization and there are no explanation.*** 

***I have explained each and every line in this article.***

It is advisable to watch video, since it help you to better understand the process.
{: .notice--warning}

## VBA Macro

Below is the ***VBA macro*** for *Unfix Component*.

```vb
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc

' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2

' Program to Unfix Component in assembly
Sub main()

  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox "Solidworks document is not opened."
    Exit Sub
  End If
  
  ' Set Solidworks Assembly document
  Set swAssembly = swDoc
  
  ' Variable for Top Level components List
  Dim vComponents As Variant
  
  ' Get Components list in opened assembly
  vComponents = swAssembly.GetComponents(True)
  
  ' Variable for Looping
  Dim index As Integer
  
  ' Loop through Components List
  For index = 0 To UBound(vComponents)
    
    ' Set Solidworks Component variable
    Set swComponent = vComponents(index)
    
    ' Check if this component is fixed or not
    If swComponent.IsFixed Then
      
      ' Select this component
      swComponent.Select True
      
      ' Unfix this component
      swAssembly.UnfixComponent
      
      ' Clear all selection
      swDoc.ClearSelection2 True
    End If
    
  Next
  
End Sub
```

## Prerequisite

There are some *prerequisite* for this article.

* Knowledge of **VBA programming language** is ‚ùó***required***.

* We use an existing part in Assembly document.

* This component is Fixed as shown in below image.

[![prerequisite](/assets/Solidworks_Images/assembly-Unfix-component/prerequisite.png)](/assets/Solidworks_Images/assembly-Unfix-component/prerequisite.png)

We will apply checks in this article, so the code we write should be error free most of the time.
{: .notice}

## Steps To Follow

This **Reference Point VBA macro** can be divided into following sections:

1. *Create and Initialize required variables*
2. *Unfix Component*

***Every section with each line is explained below.***

I also give some ***links (see icon üöÄ)*** so that you can go through them if there are anything I explained in previous articles.
{: .notice}

### Create and Initialize required variables

```vb
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: üöÄ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for *Solidworks document*. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb
' Variable for Solidworks Assembly
Dim swAssembly As SldWorks.AssemblyDoc
```

* **Purpose**: In above line, we create a variable for *Solidworks Assembly*.
* **Variable Name**: `swAssembly`
* **Type**: `SldWorks.AssemblyDoc`
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IAssemblyDoc_members.html)**.

```vb
' Variable for Solidworks Component
Dim swComponent As SldWorks.Component2
```

* **Purpose**:  In above line, we create a variable for *Solidworks Component*.
* **Variable Name**: `swComponent` 
* **Type**: `SldWorks.Component2`.
* **Reference**: Please visit üöÄ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IComponent2_members.html)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb
' Program to Unfix a Component in assembly
Sub main()

End Sub
```

* In above line, we create *main Program to Unfix a Component in assembly*.
* This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information üöÄ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

```vb
' Set Solidworks Application variable to current application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb
' Set Solidworks document variable to currently opened document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb
' Check if Solidworks document is opened or not
If swDoc Is Nothing Then
  MsgBox ("Solidworks document is not opened.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and üöÄ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened.*
  * Then we **stop** our macro here.

```vb
' Set Solidworks Assembly document
Set swAssembly = swDoc
```

* In above line, we set *value* of **`swAssembly`** variable.
* This *value* is **`swDoc`** variable.

### Unfix Component

In this section, we *Unfix Component*.

```vb
' Variable for Top Level components List
Dim vComponents As Variant
```

* **Purpose**:  In above line, we create a variable for *Top Level components List*.
* **Variable Name**: `vComponents`
* **Type**: `Variant`

```vb
' Get Components list in opened assembly
vComponents = swAssembly.GetComponents(True)
```

* In above line, we set the value of **`vComponents`** variable. 
* We set value by **`GetComponents`** method of **`swAssembly`** variable.

```vb
' Variable for Looping
Dim index As Integer
```

* **Purpose**:  In above line, we create a variable for *Looping*.
* **Variable Name**: `index`
* **Type**: `Integer`

```vb
' Loop through Components List
For index = 0 To UBound(vComponents)

Next
```

* In above line, we create a `For` loop.
* This loops start from `index = 0` to number of `UBound(vComponents)`.
  * `UBound(vComponents)` : Maximum number of component in this variable.

```vb
' Set Solidworks Component variable
Set swComponent = vComponents(index)
```

* In above line, we set *value* of **`swComponent`** variable.
* This *value* is current component inside **`vComponents`** variable.
  * We get current component by Indexing **`vComponents`** variable. 

```vb
' Check if this component is fixed or not
If swComponent.IsFixed Then
  
  ' Select this component
  swComponent.Select True
  
  ' Unfix this component
  swAssembly.UnfixComponent
  
  ' Clear all selection
  swDoc.ClearSelection2 True
End If
```

* In above code block, we check if this component is fixed or not.
* We use üöÄ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swComponent.IsFixed`**
* When this condition is **`True`**, 
  * We select current component.
  * Unfix current component.
  * Then Clear all selection.

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **Unfix component** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
