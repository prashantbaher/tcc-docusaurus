---
categories: Solidworks-macro
title:  Solidworks VBA Macro - Create Circular Pattern
permalink: /solidworks-vba-macros/create-circular-patterns/
tags:   [Solidworks Macro]
---

{% include article-notes/intermediate-note.html %}

## Objective

In this article we **create and understand** VBA macro of **Circular pattern feature** in *SOLIDWORKS CAD Software*.

This method is ***most updated*** method, so ***use this method*** if you want to create a new **Circular pattern Feature** quickly. 

## Results We Can Get

After running our macro we successfully create **Circular pattern feature** as a result.

Below image shows the result we get.

[![circular-pattern-final-result](/assets/Solidworks_Images/feature-circular-pattern/final-result-gif.gif)](/assets/Solidworks_Images/feature-circular-pattern/final-result-gif.gif)

We create **Circular pattern Feature** in following steps in general.

1. *Ask user to select **a Feature**.*
2. *Ask user to select **an edge** in direction 1.*
4. *Ask for **spacing** in direction 1.*
4. *Ask for **spacing** in direction 2.*
5. *Number of **instances** in direction 1.*
6. *Number of **instances** in direction 2.*

To get the correct result please follow the steps correctly.
{: .notice--warning}

## Macro Video

Below ðŸŽ¬ video shows **Circular pattern feature** from *SOLIDWORKS VBA Macros*.

<iframe src="https://www.youtube.com/embed/RqCEBWPNh9k" frameborder="0" allowfullscreen></iframe>


***Above video is just for visualization and there are no explanation.*** 

***I have explained each and every line in this article.***

It is advisable to watch video, since it help you to better understand the process.
{: .notice--warning}

## VBA Macro

Below is the ***VBA macro*** for creating *Circular pattern Feature*.

```vb
Option Explicit

' Variable for Solidworks Application
Dim swApp As SldWorks.SldWorks

' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2

' Variable for Solidworks Selection Manager
Dim swSelMgr As SldWorks.SelectionMgr

' Variable for Solidworks Entity
Dim swObject As SldWorks.Entity

' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData

' Variable for Circular pattern feature data
Dim swCircularPatternFeatureData As SldWorks.CircularPatternFeatureData

' Variable for Solidworks Circular pattern Feature
Dim swFeature As SldWorks.Feature

' Main program for Circular pattern
Sub main()

  ' Set Solidworks Application variable to current application
  Set swApp = Application.SldWorks
  
  ' Check if Solidworks is opened or not
  If swApp Is Nothing Then
    MsgBox ("Solidworks is not opened")
    Exit Sub
  End If
  
  ' Set Solidworks document variable to currently opened document
  Set swDoc = swApp.ActiveDoc
  
  ' Check if Solidworks document is opened or not
  If swDoc Is Nothing Then
    MsgBox ("Solidworks document is not opened. Please open a document.")
    Exit Sub
  End If
  
  ' Set Solidworks Selection Manager variable
  Set swSelMgr = swDoc.SelectionManager
  
  ' Array of Solidworks Entities
  Dim swObjects(1 To 2) As SldWorks.Entity
  
  ' Local variable for selection
  Dim selectItems As Integer
  selectItems = 1
  
  ' Loop till we select all entities
  While selectItems <= 2
    
    ' Message to show user
    Dim messageToUser As String

    ' Update Messages
    Select Case selectItems
      Case 1
        messageToUser = "Please select a Feature for Circular Pattern."
      Case 2
        messageToUser = "Please select Edge for Direction 1."
      Case Else
        Exit Sub
    End Select
  
    ' Show message to user
    MsgBox messageToUser
      
    ' Loop until we complete our selection
    While swObjects(selectItems) Is Nothing
        
      ' Local integer for loop
      Dim i As Integer
      
      ' Looping until we select
      For i = 1 To swSelMgr.GetSelectedObjectCount2(-1)
          
        ' Update Selection
        Select Case selectItems
          Case 1
            ' If the selection type body feature
            If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelBODYFEATURES Then
                
                ' Set the Solidworks Entity object to feature for Linear pattern
                Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
                
            ' If the face is selected
            ElseIf swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then
 
              ' Inform user to select feature from Feature Tree
              MsgBox "Please select Feature from Feature Tree."

              ' Clear selection
              swDoc.ClearSelection2 True
            End If
          Case 2
            ' If the edge is selected
            If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelEDGES Then
                
              ' Set the Solidworks Entity object to selected edge
              Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
            Else
              ' Inform user to select edge
              MsgBox "Please select an Edge."

              ' Clear selection
              swDoc.ClearSelection2 True
            End If

        End Select
      Next
      
      DoEvents
    Wend
    
    ' Clear previous selection
    swDoc.ClearSelection2 True
    
    ' Increase the selection count
    selectItems = selectItems + 1

  Wend
    
  ' Clear previous selection
  swDoc.ClearSelection2 True
  
  ' Local variable for counter
  Dim j As Integer
  j = 1
  
  ' Loop till counter is 2, since we have 2 selection
  While j < 3
    
    ' Set the current instance to Solidworks Entity variable
    Set swObject = swObjects(j)
    
    ' Create Select data for this entity
    Set swSelData = swSelMgr.CreateSelectData
    
    ' Update Marking as per selected entity
    Select Case j
      Case 1
        ' For feature to pattern, set mark to 4
        swSelData.Mark = 4
      Case 2
        ' For axis 1, set mark to 1
        swSelData.Mark = 1
    End Select

    ' Select the current entity
    swObject.Select4 True, swSelData
    
    j = j + 1
      
  Wend
  
  ' Local variables used as Conversion Factors
  Dim LengthConversionFactor As Double
  Dim AngleConversionFactor As Double
  
  ' Use a Select Case, to get the length of active Unit and set the different factors
  Select Case swDoc.GetUnits(0)       ' GetUnits function gives us, active unit
    
    Case swMETER    ' If length is in Meter
      LengthConversionFactor = 1
      AngleConversionFactor = 1
    
    Case swMM       ' If length is in MM
      LengthConversionFactor = 1 / 1000
      AngleConversionFactor = 1 * 0.01745329
    
    Case swCM       ' If length is in CM
      LengthConversionFactor = 1 / 100
      AngleConversionFactor = 1 * 0.01745329
    
    Case swINCHES   ' If length is in INCHES
      LengthConversionFactor = 1 * 0.0254
      AngleConversionFactor = 1 * 0.01745329
    
    Case swFEET     ' If length is in FEET
      LengthConversionFactor = 1 * (0.0254 * 12)
      AngleConversionFactor = 1 * 0.01745329
    
    Case swFEETINCHES     ' If length is in FEET & INCHES
      LengthConversionFactor = 1 * 0.0254  ' For length we use sama as Inch
      AngleConversionFactor = 1 * 0.01745329
    
    Case swANGSTROM        ' If length is in ANGSTROM
      LengthConversionFactor = 1 / 10000000000#
      AngleConversionFactor = 1 * 0.01745329
    
    Case swNANOMETER       ' If length is in NANOMETER
      LengthConversionFactor = 1 / 1000000000
      AngleConversionFactor = 1 * 0.01745329
    
    Case swMICRON       ' If length is in MICRON
      LengthConversionFactor = 1 / 1000000
      AngleConversionFactor = 1 * 0.01745329
  End Select
  
  ' Create Circular pattern feature data
  Set swCircularPatternFeatureData = swDoc.FeatureManager.CreateDefinition(swFmCirPattern)
  
  ' Set Linear pattern feature data values
  swCircularPatternFeatureData.BodyPattern = False
  swCircularPatternFeatureData.Direction2 = True
  swCircularPatternFeatureData.EqualSpacing = True
  swCircularPatternFeatureData.EqualSpacing2 = True
  swCircularPatternFeatureData.Spacing = InputBox("Please select spacing in direction 1") * AngleConversionFactor
  swCircularPatternFeatureData.Spacing2 = InputBox("Please select spacing in direction 2") * AngleConversionFactor
  swCircularPatternFeatureData.TotalInstances = InputBox("Number of instances in direction 1 including seed.")
  swCircularPatternFeatureData.TotalInstances2 = InputBox("Number of instances in direction 2 including seed.")
  
  ' Create Circular pattern
  Set swFeature = swDoc.FeatureManager.CreateFeature(swCircularPatternFeatureData)

  ' Check if Circular Pattern creates or not
  If swFeature Is Nothing Then
    MsgBox ("Failed to create Circular Pattern.")
    Exit Sub
  End If
  
  ' Erase array data
  Erase swObjects
  
  ' View zoom to fit
  swDoc.ViewZoomtofit2
  
  ' Clear all selection
  swDoc.ClearSelection2 True

End Sub
```

## Prerequisite

There are some *prerequisite* for this article.

* Knowledge of **VBA programming language** is â—***required***.
* We are **not** creating feature from code but we use existing ðŸš€ **[Extrude feature](/solidworks-macros/create-extrude-feature)** and ðŸš€ **[Hole feature](solidworks-macros/create-hole-from-hole-wizard/)** to create **Circular pattern feature**.

*Below image shown prerequisite 3D model for our demo.*

[![prerequisite](/assets/Solidworks_Images/feature-circular-pattern/prerequisite.png)](/assets/Solidworks_Images/feature-circular-pattern/prerequisite.png)

As shown in above image, there are ***2 Extrude features*** in our part.

- `Base Feature` : This is our **Base Extrude feature**.
- `Feature To Pattern` : This is our **feature for pattern**.


If you want to create these **Extrude** features ***programmatically*** then please refer to below article.

* ðŸš€ **[SOLIDWORKS Macros - Create Extrude/Boss](/solidworks-macros/create-extrude-feature)**

We will apply checks in this article, so the code we write should be error free most of the time.
{: .notice}

## Steps To Follow

This **Circular pattern Feature VBA macro** can be divided into following sections:

1. *Creating Global Variables*
2. *Initializing required variables*
3. *Ask user to select feature and directions*
4. *Mark selected entities*
5. *Get unit Conversion factors*
6. *Create ðŸš€ [Circular Pattern feature Definition](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData.html)*
7. *Create Circular Pattern feature using ðŸš€ [Circular Pattern feature definition](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData.html)*.
8. *Final work*

***Every section with each line is explained below.***

I also give some ***links (see icon ðŸš€)*** so that you can go through them if there are anything I explained in previous articles.
{: .notice}

### Creating Global Variables

```vb
Option Explicit
```

* **Purpose**: Above line forces us to define every variable we are going to use. 
* **Reference**: ðŸš€ **[SOLIDWORKS Macros - Open new Part document](/solidworks-macros/open-new-document)** article.

```vb
' Variable for Solidworks application
Dim swApp As SldWorks.SldWorks
```

* **Purpose**: In above line, we create a variable for *Solidworks application*.
* **Variable Name**: `swApp`
* **Type**: `SldWorks.SldWorks`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISldWorks_members.html)**.

```vb
' Variable for Solidworks document
Dim swDoc As SldWorks.ModelDoc2
```

* **Purpose**: In above line, we create a variable for Solidworks document. 
* **Variable Name**: `swDoc` 
* **Type**: `SldWorks.ModelDoc2`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IModelDoc2_members.html)**.

```vb
' Variable for Solidworks Selection Manager
Dim swSelMgr As SldWorks.SelectionMgr
```

* **Purpose**:  In above line, we create a variable for Solidworks Selection Manager.
*  **Variable Name**: `swSelMgr` 
* **Type**: `SldWorks.SelectionMgr`.
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr_members.html)**.

```vb
' Variable for Solidworks Entity
Dim swObject As SldWorks.Entity
```

* **Purpose**: In above line, we create a variable for *selection and marking* of Solidworks Entity.
* **Variable Name**: `swObject`
* **Type**: `SldWorks.Entity`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**.

```vb
' Variable for Solidworks Select Data
Dim swSelData As SldWorks.SelectData
```

* **Purpose**: In above line, we create a variable for **Marking** of Solidworks Select Data.
* **Variable Name**: `swSelData`
* **Type**: `SldWorks.SelectData`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectData_members.html)**.

```vb
' Variable for Circular pattern feature data
Dim swCircularPatternFeatureData As SldWorks.CircularPatternFeatureData
```

* **Purpose**: In above line, we create a variable for ðŸš€ *[Circular Pattern feature Definition](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData.html)*.
* **Variable Name**: `swCircularPatternFeatureData`
* **Type**: `SldWorks.CircularPatternFeatureData`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData.html)**.

```vb
' Variable for Solidworks Circular pattern Feature
Dim swFeature As SldWorks.Feature
```

* **Purpose**: In above line, we create a variable for *Solidworks Circular pattern Feature*.
* **Variable Name**: `swFeature`
* **Type**: `SldWorks.Feature`
* **Reference**: Please visit ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeature_members.html)**.

***These all are our global variables.***

They are **SOLIDWORKS API Objects**.

```vb
' Main program for Circular pattern
Sub main()

End Sub
```

* In above line, we create *main program for Circular pattern feature*.
*  This is a **`Sub`** procedure which has name of **`main`**. 
* This procedure hold all the *statements (instructions)* we give to computer.
* **Reference**: Detailed information ðŸš€ **[VBA Sub and Function Procedures](/vba/sub-and-function-procedure/)** article of this website.

### Initializing Required Variables

Inside this section we initialize required variables.

```vb
' Set SOLIDWORKS variable to SOLIDWORKS application
Set swApp = Application.SldWorks
```

* In above line, we set *value* of **`swApp`** variable.
* This *value* is currently opened Solidworks application.

```vb
' Check if SOLIDWORKS is opened or not
If swApp Is Nothing Then
  MsgBox ("SOLIDWORKS is not opened")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swApp`** variable.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swApp Is Nothing`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS is not opened*
  * Then we **stop** our macro here.

```vb
' Set SOLIDWORKS document variable to opened part document
Set swDoc = swApp.ActiveDoc
```

* In above line, we set *value* of **`swDoc`** variable.
* This *value* is currently *opened part document*.

```vb
' Check if SOLIDWORKS document is opened or not
If swDoc Is Nothing Then
  MsgBox ("SOLIDWORKS document is not opened. Please open a document.")
  Exit Sub
End If
```

* In above code block, we check if we successfully set the value of **`swDoc`** variable.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swDoc Is Nothing`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *SOLIDWORKS document is not opened. Please open a document.*
  * Then we **stop** our macro here.

```vb
' Set SOLIDWORKS Selection Manager variable
Set swSelMgr = swDoc.SelectionManager
```

* In above line, we set *value* of **`swSelMgr`** variable.
* This *value* is current document's *Selection Manager*.

```vb
' Array of Solidworks Entities
Dim swObjects(1 To 2) As SldWorks.Entity
```

* In above line, we create an ðŸš€ **[Array](/vba/arrays/)** of *Solidworks Entities*.
* **Variable Name**: `swObjects`
* **Type**: `SldWorks.Entity`
* **Length of Array**: *(1 To 2)*
* **Reference**: Please visit
  * **For Solidworks Entities**: ðŸš€ **[online SOLIDWORKS API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IEntity_members.html)**.
  * **For Arrays in VBA** : ðŸš€ **[Array on this website](/vba/arrays/)**.

```vb
' Local variable for selection
Dim selectItems As Integer
selectItems = 1
```

* In above line, we create a variable for selection and set its value.
* **Variable Name**: `selectItems`
* **Type**: `Integer`
*  **Value**: **`1`**

### Ask user to select feature and directions

In this section, we ask user to select **feature and directions** for *Circular Pattern feature*.

```vb
' Loop till we select all entities
While selectItems <= 2

Wend
```

* In above line of code we start a `While` loop.
* We loop until `selectItems` variable's value is equal to **2**.
* **Reference**: ðŸš€ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Message to show user
Dim messageToUser As String
```

* In above line, we create a variable to show a messages we want to show before selection.
* **Variable Name**: `messageToUser`
* **Type**: `String`

```vb
' Update Messages
Select Case selectItems
  Case 1
    messageToUser = "Please select a Feature for Circular Pattern."
  Case 2
    messageToUser = "Please select Edge for Direction 1."
  Case Else
    Exit Sub
End Select
```

* In above code block, we use a `Select` statement to update message.
* We use the `case` on `selectItems`.
  * **Case 1**: `messageToUser` update to `"Please select a Feature for Circular Pattern."`
  * **Case 2**: `messageToUser` update to `"Please select Edge for Direction 1."`
* **Reference**:  ðŸš€ **[Select statement](/vba/if-then-structure-select-case/#select-case-example)** article of this website.

```vb
' Show message to user
MsgBox messageToUser
```

* In above line, we show the message to user.
* Below image shows the message for **Feature selection** to the user.

[![message-to-select-feature-for-circular-pattern](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-feature-for-circular-pattern.png)](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-feature-for-circular-pattern.png)

* Below image shows the message for **Edge for Direction 1** to the user.

[![message-to-select-edge-for-direction-1](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-edge-for-direction-1.png)](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-edge-for-direction-1.png)

```vb
' Loop until we complete our selection
While swObjects(selectItems) Is Nothing
    
Wend
```

* In above line, we create another `While` loop.
* This loops continues to run until we select the our **Feature** and **Edge** for circular pattern.
* **Reference**: ðŸš€ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Local integer for loop
Dim i As Integer
```

* In above line, we create a variable as a counter.
* **Variable Name**: `i`
* **Type**: `Integer`

```vb
' Loop until we select
For i = 1 To swSelMgr.GetSelectedObjectCount2(-1)

Next
```

* In above line, we create a `For` loop.
* This loops start from `i = 1` to number of `objects` we select.

```vb
' Update Messages
Select Case selectItems

End Select
```

* In above line, we use a `Select` statement for selection entities.
* We use the `case` on `selectItems`.
* **Reference**:  ðŸš€ **[Select statement](/vba/if-then-structure-select-case/#select-case-example)** article of this website.

```vb
Case 1
	' If the profile sketch is selected
  If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelBODYFEATURES Then

    ' Set the Solidworks Entity object to feature for Linear pattern
    Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)

    ' If the face is selected
  ElseIf swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelFACES Then

    ' Inform user to select feature from Feature Tree
    MsgBox "Please select Feature from Feature Tree."

    ' Clear selection
    swDoc.ClearSelection2 True
  End If
```

* Above code block we use for **`Case 1`**.
* In above code block, we use  ðŸš€ **[IF/ElseIf statement](/vba/if-then-structure-select-case/)**  with conditions.
* The  ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)**  is `True` when selected `object` is type of ***SOLIDWORKS Body Feature***.
* Similarly,  ðŸš€ **[ElseIf statement](/vba/if-then-structure-select-case/)**  is `True` when select `object` is type of ***SOLIDWORKS Face***.

When we select the sketch from Model view, then selected object is *SOLIDWORKS Face*.

When we select the sketch from Feature Tree, then select object is *SOLIDWORKS Body Feature*.
{: .notice}

```vb
' Set the Solidworks Entity object to feature for Linear pattern
Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
```

* When  ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** is **`True`** we run above line.
* In above line, we set the object inside array.
  * *Array of SOLIDWORKS Entities* = `swObjects`
  * *Indexed Entity Object* = `swObjects[selectItems]`
* **Indexed Entity Object** value set by `GetSelectedObject6()` method.
  * `GetSelectedObject6()` method is part of **SOLIDWORKS Selection Manager**.
  * **Reference**: Please visit ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr~GetSelectedObject6.html)**.

```vb
' Inform user to select feature from Feature Tree
MsgBox "Please select feature from Feature Tree."

' Clear selection
swDoc.ClearSelection2 True
```

* When ðŸš€ **[ElseIf statement](/vba/if-then-structure-select-case/)** is **`True`** we execute run above lines.
* **1st Line**: We show a message to user to select **Feature** from *Feature Tree*.
* **2nd Line**: After that we clear our selection.

We do this because if selected object type is *SOLIDWORKS Face* then we can not assign this object as SOLIDWORKS Entity inside array.
{: .notice}

```vb
Case 2
  ' If the edge is selected
  If swSelMgr.GetSelectedObjectType3(i, -1) = swSelectType_e.swSelEDGES Then
  
    ' Set the Solidworks Entity object to selected edge
    Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)

  Else
    ' Inform user to select edge
    MsgBox "Please select a Edge."

    ' Clear selection
    swDoc.ClearSelection2 True
  End If
```

* Above code block we use for **`Case 2`**.
* In above line, we use  ðŸš€ **[IF/Else statement](/vba/if-then-structure-select-case/)**  with *conditions*.
* ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)**  is `True` when selected `object` is type of *SOLIDWORKS Edge*.
* If selected `object` is **not** type of *SOLIDWORKS Edge* then ðŸš€ **[Else statement](/vba/if-then-structure-select-case/)** code runs.

```vb
' Set the Solidworks Entity object to selected Edge
Set swObjects(selectItems) = swSelMgr.GetSelectedObject6(i, -1)
```

* When ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** is `True` we execute code given below.
* In above line, we set the indexed object inside array.
  * *Array of SOLIDWORKS Entities* = `swObjects`
  * *Indexed Entity Object* = `swObjects[selectItems]`
* **Indexed Entity Object** value set by `GetSelectedObject6()` method.
  * `GetSelectedObject6()` method is part of **SOLIDWORKS Selection Manager**.
  * **Reference**: Please visit ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ISelectionMgr~GetSelectedObject6.html)**.

```vb
' Inform user to select edge
MsgBox "Please select a Edge."

' Clear selection
swDoc.ClearSelection2 True
```

* When ðŸš€ **[Else statement](/vba/if-then-structure-select-case/)** is `True` we execute code given below.
* **1st Line**: We show a message to user to select **Edge**. 
* **2nd Line**: We clear our selection.

```vb
DoEvents
```

* After `For` loop end, we have a call for **`DoEvents`** function.
* This function repeats the `While` loop until we complete **Feature or Edge** selection.

```vb
' Clear previous selection
swDoc.ClearSelection2 True

' Increase the selection count
selectItems = selectItems + 1
```

* **1st Line**: We clear previous selection.
* **2nd Line**: Increment the **`selectItems`** count by 1.

After increment the selected count we continue our `while` loop.

### Mark selected Entities

In previous section, we complete our selection.

In this section, we complete **Select and Mark entities** correctly from our Array.

```vb
' Local variable for counter
Dim j As Integer
j = 1
```

* In above line, we create a variable as a counter.
* **Variable Name**: `j`
* **Type**: `Integer`
* **Value**: **`1`**

```vb
' Loop till counter is 2, since we have 2 selection
While j < 3

Wend
```

* In above line, we create a `while` loop.
* **Condition**: that *counter value* of **`j`** should be *less than 3.*
* **Reference**: ðŸš€ **[VBA Looping](/vba/looping/)** article from this website.

```vb
' Set the current instance to Solidworks Entity variable
Set swObject = swObjects(j)
```

* In above line, we set the SOLIDWORKS Entity variable to current object from array.

```vb
' Create Select data for this entity
Set swSelData = swSelMgr.CreateSelectData
```

* In above line, we create SOLIDWORKS Select Data for current SOLIDWORKS Entity object.

```vb
' Update Messages
Select Case j

End Select
```

* In above line of code, we use a `Select` statement for selection entities.
* We use the `case` on `j`.
* **Reference**:  ðŸš€ **[Select statement](/vba/if-then-structure-select-case/#select-case-example)** article of this website.

```vb
Case 1
  ' For feature to pattern, set mark to 4
  swSelData.Mark = 4
```

* Above code block, we use for **`Case 1`**.
* In above lines, we set **Mark** to **`4`**.
* For ***Circular Pattern*** feature, **`Mark`** values can be follows:

| Any...             | Must be preselected and marked with a value of... |
| ------------------ | ------------------------------------------------- |
| Direction 1        | 1                                                 |
| Feature to pattern | 4                                                 |

* **Reference**: Please visit ðŸš€ **[Circular Pattern Feature](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.interop.sldworks~SolidWorks.interop.sldworks.ICircularPatternFeatureData.html)** in *SOLIDWORS API Help*.

```vb
Case 2
  ' For direction 1, set mark to 1
  swSelData.Mark = 1
```

* Above code sample we use for **`Case 2`**.
* In above lines, we update the **Mark** to **`1`**.
* For ***Circular Pattern*** feature, **`Mark`** values can be follows:

| Any...             | Must be preselected and marked with a value of... |
| ------------------ | ------------------------------------------------- |
| Direction 1        | 1                                                 |
| Feature to pattern | 4                                                 |

* **Reference**: Please visit ðŸš€ **[Circular Pattern Feature](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.interop.sldworks~SolidWorks.interop.sldworks.ICircularPatternFeatureData.html)** in *SOLIDWORS API Help*.

```vb
' Select the current entity
swObject.Select4 True, swSelData
```

* In above line, we select the object.
* We select it by using `Select4` method of *SOLIDWORKS Entity object*.
* This method takes 2 arguments.
  * **Append** - *`True` appends the entity to the selection list, `False` replaces the selection list with this entity.*
  * **Data** - *Pointer to the **`ISelectData`** object.*
* We use following values as parameter.
  * **Append** - **`True`**
  * **Data** - **`swSelData`**

```vb
j = j + 1
```

After **`Select`** statement, we increment the counter variable **`j`** by **1**.

### Get unit Conversion factors

In this section we get unit Conversion factors.

```vb
'-----------------------Unit Conversion Factors----------------------------------------
  
' Local variables used as Conversion Factors
Dim LengthConversionFactor As Double
Dim AngleConversionFactor As Double

' Use a Select Case, to get the length of active Unit and set the different factors
Select Case swDoc.GetUnits(0)       ' GetUnits function gives us, active unit

Case swMETER    ' If length is in Meter
  LengthConversionFactor = 1
  AngleConversionFactor = 1

Case swMM       ' If length is in MM
  LengthConversionFactor = 1 / 1000
  AngleConversionFactor = 1 * 0.01745329

Case swCM       ' If length is in CM
  LengthConversionFactor = 1 / 100
  AngleConversionFactor = 1 * 0.01745329

Case swINCHES   ' If length is in INCHES
  LengthConversionFactor = 1 * 0.0254
  AngleConversionFactor = 1 * 0.01745329

Case swFEET     ' If length is in FEET
  LengthConversionFactor = 1 * (0.0254 * 12)
  AngleConversionFactor = 1 * 0.01745329

Case swFEETINCHES     ' If length is in FEET & INCHES
  LengthConversionFactor = 1 * 0.0254  ' For length we use sama as Inch
  AngleConversionFactor = 1 * 0.01745329

Case swANGSTROM        ' If length is in ANGSTROM
  LengthConversionFactor = 1 / 10000000000#
  AngleConversionFactor = 1 * 0.01745329

Case swNANOMETER       ' If length is in NANOMETER
  LengthConversionFactor = 1 / 1000000000
  AngleConversionFactor = 1 * 0.01745329

Case swMICRON       ' If length is in MICRON
  LengthConversionFactor = 1 / 1000000
  AngleConversionFactor = 1 * 0.01745329
End Select

'----------------------------------------------------------------
```

* I have already explained about this in previous ðŸš€ **[Solidworks Macro - Fix Unit Issue](/solidworks-macros/unit-correction/)** article in this website.
* Please visit ðŸš€ **[Solidworks Macro - Fix Unit Issue](/solidworks-macros/unit-correction/)** article for more details.

### Create Circular Pattern Feature Definition 

In this section we create  ðŸš€ *[Circular Pattern feature Definition](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData.html)* and set its properties for **Circular Pattern**.

```vb
' Create Circular pattern feature data
Set swCircularPatternFeatureData = swDoc.FeatureManager.CreateDefinition(swFmCirPattern)
```

* In above line of code we set the value of variable `swCircularPatternFeatureData` by `CreateDefinition` method.
* `CreateDefinition` method is part of `FeatureManager` object.
* `FeatureManager` is again part of `swDoc` variable i.e. `ModelDoc2` object.
This `CreateDefinition` method takes following parameters as explained:
  - **Type** - *`Feature name ID` as defined in `swFeatureNameID_e`*.
    - `swFmBoundingBox` *(bounding box)*
    - `swFmCirPattern` *(circular pattern)*
    - `swFmCurvePattern` *(curve-driven pattern)*
    - `swFmDerivedLPattern` *(derived-driven pattern)*
    - `swFmDimPattern` *(variable/dimension pattern)*
    - `swFmFillPattern` *(fill pattern)*
    - `swFmGroundPlane` *(ground plane)*
    - `swFmLibraryFeature` *(library)*
    - `swFmLocalChainPattern` *(chain component pattern)*
    - `swFmLocalCirPattern` *(circular component pattern)*
    - `swFmLocalCurvePattern` *(curve-driven component pattern)*
    - `swFmLocalLPattern` *(linear component pattern)*
    - `swFmLocalSketchPattern` *(sketch-driven component pattern)*
    - `swFmLPattern` *(linear pattern)*
    - `swFmNormalCut` *(sheet metal normal cut)*
    - `swFmRefCurve` *(projection curve)*
    - `swFmRefSurface` *(surface sweep)*
    - `swFmSketchPattern` *(sketch-driven pattern)*
    - `swFmSweep` *(boss sweep)*
    - `swFmSweepCut` *(cut sweep)*
    - `swFmSweepThread` *(Thread)*
    - `swFmTabAndSlot` *(tab and slot)*
    - `swFmTablePattern` *(table pattern)*

* **Return Value** : This `CreateDefinition` method return **feature or pattern-specific** feature data object.
* In our code, I have used following values:
    * **Type** - *I use `swFmCirPattern` as `Feature name ID`*.

**Reference**: For more details about 

* **Solidworks Feature Manager details**: ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeatureManager_members.html)** for Solidworks Feature Manager.
* **Circular Pattern Feature data**: ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData.html)** for Circular Pattern Feature data.

```vb
swCircularPatternFeatureData.BodyPattern = False
```

* In above line, we set value of `BodyPattern` to `False`.

```vb
swCircularPatternFeatureData.Direction2 = True
```

* In above line code, we set value of `D1ReverseDirection` to `True`.
* This allows us to pattern in **second direction** also.

```vb
swCircularPatternFeatureData.EqualSpacing = True
```

* In above line code, we set value of `EqualSpacing` to `True`.
* This allows us to set pattern instances at **equal spacing** in **direction 1**.

```vb
swCircularPatternFeatureData.EqualSpacing2 = True
```

* In above line code, we set value of `EqualSpacing2` to `True`.
* This allows us to set pattern instances at **equal spacing** in **direction 2**.

```vb
swCircularPatternFeatureData.Spacing = InputBox("Please select spacing in direction 1") * AngleConversionFactor
```

* In above line of code we are doing **3 steps** in one line.

Those 3 steps are explained below.

* **Step 1** - Getting **direction 1 spacing** from user.
Below image shows the message for **direction 1 spacing** to the user.

[![message-to-select-spacing-for-direction-1](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-spacing-for-direction-1.png)](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-spacing-for-direction-1.png)

* **Step 2** - Convert input spacing as per document unit system.
* **Step 3** - Assigned converted value to **`swCircularPatternFeatureData.Spacing`** property.

```vb
swCircularPatternFeatureData.Spacing2 = InputBox("Please select spacing in direction 2") * AngleConversionFactor
```

In above line of code we are doing **3 steps** in one line.

Those 3 steps are explained below.

* **Step 1** - Getting **direction 2 spacing** from user.
Below image shows the message for **direction 2 spacing** to the user.

[![message-to-select-spacing-for-direction-2](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-spacing-for-direction-2.png)](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-spacing-for-direction-2.png)

* **Step 2** - Convert input spacing as per document unit system.
* **Step 3** - Assigned converted value to **`swCircularPatternFeatureData.Spacing2`** property.

```vb
swCircularPatternFeatureData.TotalInstances = InputBox("Number of instances in direction 1 including seed.")
```

* In above line of code we are doing **2 steps** in one line.

Those 2 steps are explained below.

* **Step 1** - Getting **number of instances in direction 1** from user.
Below image shows the message for **number of instances 1 spacing** to the user.

[![message-to-select-number-of-instance-for-direction-1](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-number-of-instance-for-direction-1.png)](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-number-of-instance-for-direction-1.png)

* **Step 2** - Assigned input value to **`swCircularPatternFeatureData.TotalInstances`** property.

```vb
swCircularPatternFeatureData.TotalInstances2 = InputBox("Number of instances in direction 2 including seed.")
```

* In above line of code we are doing **2 steps** in one line.

Those 2 steps are explained below.

* **Step 1** - Getting **number of instances in direction 2** from user.

Below image shows the message for **number of instances 2 spacing** to the user.

[![message-to-select-number-of-instance-for-direction-2](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-number-of-instance-for-direction-2.png)](/assets/Solidworks_Images/feature-circular-pattern/message-to-select-number-of-instance-for-direction-2.png)

* **Step 2** - Assigned input value to **`swCircularPatternFeatureData.TotalInstances2`** property.

### Create Circular Pattern

In this section, we create Circular Pattern.

```vb
' Create Circular pattern
Set swFeature = swDoc.FeatureManager.CreateFeature(swCircularPatternFeatureData)
```

* In above line, we set the value of variable `swFeature` by `CreateFeature` method.
* This `CreateFeature` method takes following parameters as explained:
    * **FeatureData** - *`Feature or pattern-specific` feature data object*.
* **Return Value** : This `CreateFeature` method return **feature** data object.
  * `CreateFeature` method is part of `FeatureManager` object.
  * This `FeatureManager` is again part of `swDoc` variable i.e. `ModelDoc2` object.

* **Reference**: ðŸš€ **[online Solidworks API Help](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.IFeatureManager_members.html)** for Solidworks Feature Manager `FeatureManager`.

* In our code, I have used following values:
    * **FeatureData** - *I use `swCircularPatternFeatureData` as feature data object which we defined previously*.

```vb
' Check if Circular Pattern creates or not
If swFeature Is Nothing Then
  MsgBox ("Failed to create Circular Pattern.")
  Exit Sub
End If
```

* In above code block, we check if we successfully create ðŸš€ **[Circular Pattern Feature](https://help.solidworks.com/2019/english/api/sldworksapi/SolidWorks.Interop.sldworks~SolidWorks.Interop.sldworks.ICircularPatternFeatureData_members.html)** or not.
* We use ðŸš€ **[IF statement](/vba/if-then-structure-select-case/)** for checking.
* **Condition**: **`swFeature Is Nothing`**
* When this condition is **`True`**, 
  * We show and ðŸš€ **[message window](/vba/msgBox-function/)** to user.
  * **Message**: *Failed to create Circular Pattern.*
  * Then we **stop** our macro here.

[![circular-pattern-final-result](/assets/Solidworks_Images/feature-circular-pattern/final-result-gif.gif)](/assets/Solidworks_Images/feature-circular-pattern/final-result-gif.gif)

### Final work

In this section, after creating **Circular Pattern** feature, we have to do some cleaning work so that we can use this macro frequently.

```vb
' Erase array data
Erase swObjects
```

* In above line, we **erase** data from *SOLIDWORKS Entity array*.
* For this we use **`Erase`** function in-build in VBA.

```vb
' View zoom to fit
swDoc.ViewZoomtofit2
```

* In above line, we make our view zoom to fit the model.
* For this we use `ViewZoomtofit2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

```vb
' Clear all selection
swDoc.ClearSelection2 True
```

* In above line, we clear all previous selection.
* For this we use `ClearSelection2` method which is part of *SOLIDWORKS Document* variable i.e `swDoc` variable.

**This is it !!!**

*I hope my efforts will helpful to someone!*

If you found anything to **add or update**, please let me know on my *e-mail*.

Hope this post helps you to **create Circular Pattern feature** with SOLIDWORKS VBA Macros.

For more such tutorials on **SOLIDWORKS VBA Macro**, do come to this website after sometime.

*If you like the post then please share it with your friends also.*

*Do let me know by you like this post or not!*

*Till then, Happy learning!!!*
